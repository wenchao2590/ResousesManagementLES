CREATE PROCEDURE [LES].[PROC_SPM_SAP_LES_BARCODE]
(
	@runsheetSN INT
)
AS
BEGIN
	DECLARE spm_barcode_crsr CURSOR FAST_FORWARD FOR 
	SELECT
		B.[PLAN_RUNSHEET_NO],
		A.[WM_NO],
		A.[ZONE_NO],
		B.[PLANT_ZONE],
		A.[SUPPLIER_NUM],
		A.[PART_NO],
		A.[PART_CNAME],
		A.[BOX_PARTS],
		A.[PICKUP_SEQ_NO],
		A.[DLOC],
		A.[MEASURING_UNIT_NO],
		A.[INHOUSE_PACKAGE_MODEL],
		A.[REQUIRED_INHOUSE_PACKAGE_QTY],
		A.[PLANT],
		A.[DOCK],
		A.[INHOUSE_PACKAGE],
		B.[EXPECTED_ARRIVAL_TIME],
		(SELECT TOP 1 [SUPPLIER_NAME] FROM [LES].[TM_BAS_SUPPLIER] C WITH (NOLOCK) WHERE A.[SUPPLIER_NUM] = C.[SUPPLIER_NUM]) AS SUPPLIER_NAME,
		(SELECT TOP 1 [PART_NICKNAME] FROM [LES].[TM_BAS_MAINTAIN_PARTS] D WITH (NOLOCK) WHERE A.[PLANT] = D.[PLANT] AND A.[PART_NO] = D.[PART_NO]) AS PART_NICKNAME,
		(SELECT TOP 1 [DLOC] FROM [LES].[TM_BAS_PARTS_STOCK] S WITH (NOLOCK) WHERE S.[PART_NO] = A.[PART_NO] AND S.[WM_NO] = A.[WM_NO] AND S.[ZONE_NO]=M.[REPACKAGE_ZONE] and S.[PLANT]=A.[PLANT]) AS INNER_LOCATION--根据存储区查找对应翻包存储区的库位
	FROM [LES].[TT_SPM_DELIVERY_RUNSHEET_DETAIL] A WITH (NOLOCK)
	INNER JOIN [LES].[TT_SPM_DELIVERY_RUNSHEET] B WITH (NOLOCK) ON B.[PLAN_RUNSHEET_SN] = A.[PLAN_RUNSHEET_SN]
	LEFT JOIN [LES].[TM_WMM_ZONES] M WITH (NOLOCK) ON M.[ZONE_NO] = A.[ZONE_NO] AND M.[WM_NO] = A.[WM_NO] AND M.[PLANT] = A.[PLANT]
	WHERE A.[PLAN_RUNSHEET_SN] = @runsheetSN

	DECLARE @PLAN_RUNSHEET_NO NVARCHAR(30)
	DECLARE @WM_NO NVARCHAR(10)
	DECLARE @ZONE_NO NVARCHAR(20)
	DECLARE @PLANT_ZONE NVARCHAR(20)
	DECLARE @SUPPLIER_NUM NVARCHAR(12)
	DECLARE @PART_NO NVARCHAR(20)
	DECLARE @PART_CNAME NVARCHAR(300)
	DECLARE @BOX_PARTS NVARCHAR(10)
	DECLARE @PICKUP_SEQ_NO INT
	DECLARE @DLOC NVARCHAR(20)--库位地址
	DECLARE @MEASURING_UNIT_NO NVARCHAR(1)
	DECLARE @INHOUSE_PACKAGE_MODEL NVARCHAR(30)
	DECLARE @REQUIRED_INHOUSE_PACKAGE_QTY INT
	DECLARE @PLANT NVARCHAR(5)
	DECLARE @DOCK NVARCHAR(10)
	DECLARE @INHOUSE_PACKAGE INT
	DECLARE @barCodeNo NVARCHAR(32)
	DECLARE @EXPECTED_ARRIVAL_TIME DATETIME
	DECLARE @SUPPLIER_NAME NVARCHAR(100)
	DECLARE @PART_NICKNAME NVARCHAR(50)
	DECLARE @INNER_LOCATION NVARCHAR(50)--超市地址
	DECLARE @LINE_POSITION NVARCHAR(100)--线边地址

	OPEN spm_barcode_crsr
	FETCH NEXT FROM spm_barcode_crsr INTO 
	@PLAN_RUNSHEET_NO,
	@WM_NO,
	@ZONE_NO,
	@PLANT_ZONE,
	@SUPPLIER_NUM,
	@PART_NO,
	@PART_CNAME,
	@BOX_PARTS,
	@PICKUP_SEQ_NO,
	@DLOC,
	@MEASURING_UNIT_NO,
	@INHOUSE_PACKAGE_MODEL,
	@REQUIRED_INHOUSE_PACKAGE_QTY,
	@PLANT,
	@DOCK,
	@INHOUSE_PACKAGE,
	@EXPECTED_ARRIVAL_TIME,
	@SUPPLIER_NAME,
	@PART_NICKNAME,
	@INNER_LOCATION
	WHILE( @@fetch_status = 0 )
		BEGIN
			IF @PLANT_ZONE = '2016'
				BEGIN
					--获取库位地址
					SELECT TOP 1 @DLOC = [DLOC] FROM [LES].[TM_BAS_PARTS_STOCK] WITH (NOLOCK) WHERE  [PLANT] = @PLANT AND [ZONE_NO] = '2015' AND [PART_NO] = @PART_NO
					--获取超市地址
					SELECT TOP 1 @INNER_LOCATION = [DLOC] FROM [LES].[TM_BAS_PARTS_STOCK] WITH (NOLOCK) WHERE  [PLANT] = @PLANT AND [ZONE_NO] = '2016' AND [PART_NO] = @PART_NO
				END

			--根据目的存储区获取 生产线 然后再加上生产线 到拉动关系中获取 线边地址
			SELECT TOP 1
				@LINE_POSITION=L.STORAGE_LOCATION 
			FROM [LES].[TM_BAS_PARTS_STOCK] S WITH (NOLOCK)
			INNER JOIN [LES].[TM_BAS_MAINTAIN_INHOUSE_LOGISTIC_STANDARD] L WITH (NOLOCK) ON L.[PLANT] = S.[PLANT] AND L.[PART_NO] = S.[PART_NO] AND L.[ASSEMBLY_LINE] = S.[ASSEMBLY_LINE]
			WHERE S.[PLANT] = @PLANT AND S.[PART_NO] = @PART_NO AND S.[ZONE_NO] = ISNULL(@ZONE_NO, '')

			IF	(SELECT COUNT(1) FROM 
				[LES].[TM_BAS_PARTS_STOCK] S WITH (NOLOCK)
				INNER JOIN [LES].[TM_BAS_MAINTAIN_INHOUSE_LOGISTIC_STANDARD] L WITH (NOLOCK)
				ON L.[PLANT] = S.[PLANT] AND L.[PART_NO] = S.[PART_NO] AND L.[ASSEMBLY_LINE] = S.[ASSEMBLY_LINE]
				WHERE S.[PLANT] = @PLANT AND S.[PART_NO] = @PART_NO AND S.[ZONE_NO] = ISNULL(@ZONE_NO, ''))>1
				BEGIN
					SET @LINE_POSITION = @LINE_POSITION + '...'
				END

			WHILE (@REQUIRED_INHOUSE_PACKAGE_QTY > 0)		--循环创建BARCODE
				BEGIN			
					EXEC [LES].[PROC_SPM_GET_NEXT_BARCODE_SEQUENCE] 'BARCODE', @barCodeNo OUTPUT

					INSERT INTO [LES].[TT_SPM_DELIVERY_RUNSHEET_BARCODE]
					(
						[PLAN_ASN_RUNSHEET_NO],
						[SUPPLIER_NUM],
						[PART_NO],
						[PART_CNAME],
						[BOX_PARTS],
						[PICKUP_SEQ_NO],
						[MEASURING_UNIT_NO],
						[INHOUSE_PACKAGE_MODEL],
						[REQUIRED_INBOUND_PACKAGE_QTY],
						[PLANT],
						[DOCK],
						[BARCODE_DATA],
						[REQUIRED_DATE],
						[SUPPLIER_NAME],
						[STORAGE_LOCATION],
						[PART_NICKNAME],
						[INNER_LOCATION],
						[LINE_POSITION]
					) 
					VALUES 
					(
						@PLAN_RUNSHEET_NO,
						@SUPPLIER_NUM,
						@PART_NO,
						@PART_CNAME,
						'DS',
						@PICKUP_SEQ_NO,
						@MEASURING_UNIT_NO,
						@INHOUSE_PACKAGE_MODEL,
						@INHOUSE_PACKAGE,
						@PLANT,
						@DOCK,
						@barCodeNo,
						@EXPECTED_ARRIVAL_TIME,
						@SUPPLIER_NAME,
						@DLOC,
						@PART_NICKNAME,
						@INNER_LOCATION,
						@LINE_POSITION
					)
		
					SET @REQUIRED_INHOUSE_PACKAGE_QTY = @REQUIRED_INHOUSE_PACKAGE_QTY - 1
				END

			FETCH NEXT FROM spm_barcode_crsr INTO 
			@PLAN_RUNSHEET_NO,
			@WM_NO,
			@ZONE_NO,
			@PLANT_ZONE,
			@SUPPLIER_NUM,
			@PART_NO,
			@PART_CNAME,
			@BOX_PARTS,
			@PICKUP_SEQ_NO,
			@DLOC,
			@MEASURING_UNIT_NO,
			@INHOUSE_PACKAGE_MODEL,
			@REQUIRED_INHOUSE_PACKAGE_QTY,
			@PLANT,
			@DOCK,
			@INHOUSE_PACKAGE,
			@EXPECTED_ARRIVAL_TIME,
			@SUPPLIER_NAME,
			@PART_NICKNAME,
			@INNER_LOCATION
		END
	CLOSE  spm_barcode_crsr
	DEALLOCATE spm_barcode_crsr
END