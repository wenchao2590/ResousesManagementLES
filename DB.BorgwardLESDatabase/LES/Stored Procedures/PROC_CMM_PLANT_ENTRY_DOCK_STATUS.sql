-- =============================================
-- Author:		Scott
-- Create date: 2017/10/13
-- Description:	道口释放/占用存储过程
-- =============================================
CREATE PROCEDURE [LES].[PROC_CMM_PLANT_ENTRY_DOCK_STATUS]
	@FID UNIQUEIDENTIFIER,
	@LOGIN_USERNAME NVARCHAR(50)
AS
BEGIN
	DECLARE @DOCK_STATUS INT;
	DECLARE @DOCK NVARCHAR(16);
	DECLARE @VEHICLE_NO NVARCHAR(50);
	DECLARE @SHEET_NO NVARCHAR(50);
	DECLARE @NOW DATETIME=GETDATE();
	DECLARE @AREA_ID INT;

	--获取道口编号和道口状态
	SELECT TOP 1 @DOCK=DOCK_NO, @DOCK_STATUS=DOCK_STATUS FROM [LES].[TI_MID_RFID_DOCK_STATUS] WITH(NOLOCK) WHERE FID=@FID;

	--获取车辆和订单信息
	SELECT TOP 1 @VEHICLE_NO=VEHICLE_NO, @SHEET_NO=SHEET_NO FROM [LES].[TT_CMM_DOCK_STATUS] WHERE DOCK=@DOCK

	BEGIN TRAN
		--道口占用
		IF @DOCK_STATUS=1
		BEGIN
			--更新入厂日志表的道口占用时间和等待时间
			UPDATE [LES].[TT_CMM_PLANT_ENTRY_LOG]
			SET [DOCK_HOLD_TIME]=@NOW
			,DOCK=@DOCK
			,WAITING_TIME=DATEDIFF(MINUTE,[ENTRY_TIME],@NOW)
			,STATUS=2--在道口卸货
			WHERE VEHICLE_NO=@VEHICLE_NO AND RUNSHEET_NO=@SHEET_NO
		END
		--道口释放
		ELSE
		BEGIN
			--更新入厂日志表的道口释放时间和实际卸货时间
			UPDATE [LES].[TT_CMM_PLANT_ENTRY_LOG]
			SET [DOCK_RELEASE_TIME]=@NOW
			,[DOCK_PROCESSING_TIME]=DATEDIFF(MINUTE,[DOCK_HOLD_TIME],@NOW)
			WHERE VEHICLE_NO=@VEHICLE_NO AND RUNSHEET_NO=@SHEET_NO

			--获取释放道口的等待区信息
			SELECT TOP 1 @AREA_ID=AREA_ID
			FROM [LES].[TM_BAS_DOCK] AS DOCK WITH(NOLOCK)
			WHERE DOCK=@DOCK

			DECLARE @NEXT_VEHICLE_NO NVARCHAR(50)=NULL;
			DECLARE @NEXT_SHEET_NO NVARCHAR(50)=NULL;

			--找到下一辆即将到道口卸货的车辆信息和订单信息
			SELECT TOP 1 @NEXT_VEHICLE_NO=VEHICLE_NO, @NEXT_SHEET_NO=RUNSHEET_NO FROM [LES].[TT_CMM_WAITING_QUEUE] WHERE AREA_ID=@AREA_ID
			ORDER BY [SEQUENCE] ASC

			--如果该等待区的队列中有等待卸货的车辆，那么让该车占用当前释放的道口
			IF @NEXT_VEHICLE_NO IS NOT NULL
			BEGIN
				--分配下一辆车到道口卸货
				UPDATE [LES].[TT_CMM_DOCK_STATUS]
				SET
				[STATUS]=1,
				VEHICLE_NO=@NEXT_VEHICLE_NO,
				SHEET_NO=@NEXT_SHEET_NO,
				UPDATE_DATE=GETDATE(),
				UPDATE_USER=@LOGIN_USERNAME
				WHERE DOCK=@DOCK

				--更新等待区队列的排队号，全部减一
				UPDATE [LES].[TT_CMM_WAITING_QUEUE]
				SET SEQUENCE=SEQUENCE-1
				WHERE AREA_ID=@AREA_ID
				
				--插入数据到短信接口表，发送短信给司机前往相应道口卸货
				DECLARE @FID_DOCK_HOLD_MESSAGE UNIQUEIDENTIFIER=NEWID();

				INSERT INTO [LES].[TI_MID_RFID_BERTH_INSTRUCT](FID,CARGO_LICENCE,DRIVER_TEL,MSG_CONTEXT,MSG_DATE)
				VALUES(@FID_DOCK_HOLD_MESSAGE,@NEXT_VEHICLE_NO,@NEXT_SHEET_NO,N'请司机前往道口【'+ISNULL(@DOCK,N'')+N'】开始卸货',GETDATE());

				EXEC [LES].[PROC_SYS_OUTBOUND_INSERT] @FID_DOCK_HOLD_MESSAGE,N'003',N'TI_MID_RFID_BERTH_INSTRUCT',N'DOCK|STATUS|URGENT',@LOGIN_USERNAME;
			END
			--等待队列中已经没有需要在该道口卸货的车辆了
			ELSE
			BEGIN
				--更改道口状态为释放
				UPDATE [LES].[TT_CMM_DOCK_STATUS]
				SET
				[STATUS]=0,
				VEHICLE_NO=NULL,
				SHEET_NO=NULL,
				UPDATE_DATE=GETDATE(),
				UPDATE_USER=@LOGIN_USERNAME
				WHERE DOCK=@DOCK
			END
		END
	COMMIT
END