/********************************************************************/
/*   Project Name:  TWD												*/
/*   Program Name:  [LES].[PROC_TWD_GENERATE_BARCODE_SUPPLEMENT]	*/
/*   Called By:     web page										*/
/*   Author:        孙述霄											*/
/*   Create date:	2017-06-19										*/
/*   Note:			TWD 生成补打条码								*/
/********************************************************************/
CREATE PROCEDURE [LES].[PROC_TWD_GENERATE_BARCODE_SUPPLEMENT]
(
	@RunsheetSN INT,
	@RunsheetDetailId INT,
	@PrintQty INT
)
AS
BEGIN
	SET NOCOUNT ON
	DECLARE @Plant NVARCHAR(5)
	DECLARE @AssembleLine NVARCHAR(10)
	DECLARE @TWD_RUNSHEET_NO NVARCHAR(50)
	DECLARE @DOCK NVARCHAR(10)
	DECLARE @EXPECTED_ARRIVAL_TIME DATETIME
	DECLARE @WM_NO NVARCHAR(10)
	DECLARE @PLANT_ZONE NVARCHAR(10)
	DECLARE @PART_NO NVARCHAR(20)
	DECLARE @PART_CNAME NVARCHAR(100)
	DECLARE @PART_NICKNAME NVARCHAR(50)
	DECLARE @INHOUSE_PACKAGE_MODEL NVARCHAR(30)
	DECLARE @INBOUND_PACKAGE INT
	DECLARE @SUPPLIER_NUM NVARCHAR(12)
	DECLARE @SUPPLIER_NAME NVARCHAR(100)
	DECLARE @BOX_PARTS NVARCHAR(10)
	DECLARE @MEASURING_UNIT_NO NVARCHAR(8)
	DECLARE @STORAGE_LOCATION NVARCHAR(30)
	DECLARE @LINE_POSITION NVARCHAR(100)
	DECLARE @INNER_LOCATION NVARCHAR(50)
		
	SELECT
		@plant = [PLANT],
		@AssembleLine = [ASSEMBLY_LINE],
		@TWD_RUNSHEET_NO = [TWD_RUNSHEET_NO],
		@PLANT_ZONE=[PLANT_ZONE],
		@DOCK = [DOCK],
		@EXPECTED_ARRIVAL_TIME = [EXPECTED_ARRIVAL_TIME]
    FROM [LES].[TT_TWD_RUNSHEET] WITH (NOLOCK) WHERE TWD_RUNSHEET_SN = @RunsheetSN

	SELECT
		@PART_NO = D.[PART_NO],
		@PART_CNAME = D.[PART_CNAME],
		@INHOUSE_PACKAGE_MODEL = D.[INBOUND_PACKAGE_MODEL],
		@INBOUND_PACKAGE = D.[INBOUND_PACKAGE],
		@SUPPLIER_NUM = D.[SUPPLIER_NUM],
		@SUPPLIER_NAME = S.[SUPPLIER_NAME],
		@BOX_PARTS = D.[BOX_PARTS],
		@MEASURING_UNIT_NO = D.[MEASURING_UNIT_NO]
	FROM [LES].[TT_TWD_RUNSHEET_DETAIL] D WITH (NOLOCK)
	LEFT JOIN [LES].[TM_BAS_SUPPLIER] S WITH (NOLOCK) ON D.[SUPPLIER_NUM] = S.[SUPPLIER_NUM]
	WHERE [RUNSHEET_DETAIL_ID] = @RunsheetDetailId

	IF @PLANT_ZONE = '2016'
		BEGIN
			--获取库位地址
			SELECT TOP 1 @STORAGE_LOCATION = [DLOC] FROM [LES].[TM_BAS_PARTS_STOCK] WITH (NOLOCK) WHERE  [PLANT] = @plant AND [ZONE_NO] = '2015' AND [PART_NO] = @PART_NO
			--获取超市地址
			SELECT TOP 1 @INNER_LOCATION = [DLOC] FROM [LES].[TM_BAS_PARTS_STOCK] WITH (NOLOCK) WHERE  [PLANT] = @plant AND [ZONE_NO] = '2016' AND [PART_NO] = @PART_NO
		END
	ELSE
		BEGIN
			--获取库位地址
			SELECT TOP 1 @WM_NO = [WM_NO], @STORAGE_LOCATION = [DLOC] FROM [LES].[TM_BAS_PARTS_STOCK] WITH (NOLOCK) WHERE  [PLANT] = @plant AND [ZONE_NO] = @PLANT_ZONE AND [PART_NO] = @PART_NO
			--获取超市地址
			SELECT TOP 1 @INNER_LOCATION = [DLOC] FROM [LES].[TM_BAS_PARTS_STOCK] WITH (NOLOCK) WHERE  [PLANT] = @plant AND [PART_NO] = @PART_NO AND [WM_NO] = @WM_NO AND [ZONE_NO] IN
			(
				SELECT [REPACKAGE_ZONE] FROM [LES].[TM_WMM_ZONES] WITH (NOLOCK) WHERE [PLANT] = @plant AND [WM_NO] = @WM_NO AND [ZONE_NO] = @PLANT_ZONE
			)
		END
	--从拉动关系中获取零件昵称及线边地址
	SELECT TOP 1
		@PART_NICKNAME = L.[PART_NICKNAME],
		@LINE_POSITION = L.[STORAGE_LOCATION] 
	FROM [LES].[TM_BAS_PARTS_STOCK] S WITH (NOLOCK) 
	INNER JOIN [LES].[TM_BAS_MAINTAIN_INHOUSE_LOGISTIC_STANDARD] L WITH (NOLOCK)
	ON L.[PLANT] = S.[PLANT] AND L.[PART_NO] = S.[PART_NO] and L.[ASSEMBLY_LINE] = S.[ASSEMBLY_LINE]
	WHERE S.[PLANT] = @plant AND S.[PART_NO] = @PART_NO AND S.[ZONE_NO] = ISNULL(@PLANT_ZONE, '')

	IF	(SELECT COUNT(1) FROM 
		[LES].[TM_BAS_PARTS_STOCK] S WITH (NOLOCK) 
		INNER JOIN [LES].[TM_BAS_MAINTAIN_INHOUSE_LOGISTIC_STANDARD] L WITH (NOLOCK)
		ON L.[PLANT] = S.[PLANT] AND L.[PART_NO] = S.[PART_NO] AND L.[ASSEMBLY_LINE] = S.[ASSEMBLY_LINE]
		WHERE S.[PLANT] = @plant AND S.[PART_NO] = @PART_NO AND S.[ZONE_NO] = ISNULL(@PLANT_ZONE, ''))>1
	BEGIN
		SET @LINE_POSITION = @LINE_POSITION + '...'
	END

	WHILE(@PrintQty > 0)
		BEGIN
			DECLARE @barCodeNo NVARCHAR(32) 
			DECLARE @CreatedOn DATETIME
			EXEC LES.[PROC_SYS_GET_NEXT_BARCODE_SEQUENCE] 'BARCODE', @barCodeNo OUTPUT
			SET @CreatedOn = GETDATE()
				
			INSERT INTO [LES].[TT_SPM_DELIVERY_RUNSHEET_BARCODE]
			(
				[PLAN_ASN_RUNSHEET_NO],
				[PART_NO],
				[PART_CNAME],
				[BARCODE_DATA],
				[INHOUSE_PACKAGE_MODEL],
				[REQUIRED_INBOUND_PACKAGE_QTY],
				[CREATE_DATE],
				[SUPPLIER_NUM],
				[SUPPLIER_NAME],
				[BOX_PARTS],
				[MEASURING_UNIT_NO],
				[TWD_RUNSHEET_NO],
				[PLANT],
				[STORAGE_LOCATION],
				[INNER_LOCATION],
				[PART_NICKNAME],
				[LINE_POSITION],
				[PRINT_SUPPLEMENT],
				[REQUIRED_DATE],
				[DOCK]
			)
			VALUES
			(
				@TWD_RUNSHEET_NO,
				@PART_NO,
				@PART_CNAME,
				@barCodeNo,
				@INHOUSE_PACKAGE_MODEL,
				@INBOUND_PACKAGE,
				@CreatedOn,
				ISNULL(@SUPPLIER_NUM, ''),
				ISNULL(@SUPPLIER_NAME, ''),
				@BOX_PARTS,
				ISNULL(@MEASURING_UNIT_NO, ''),
				@TWD_RUNSHEET_NO,
				@plant,
				@STORAGE_LOCATION,
				@INNER_LOCATION,
				@PART_NICKNAME,
				@LINE_POSITION,
				1,
				@EXPECTED_ARRIVAL_TIME,
				@DOCK
			)

			SET @PrintQty = @PrintQty - 1
		END

	--打印新的箱条码
	INSERT INTO [LES].[TT_WMS_AUTOPRINT_BARCODE]
	(
		[PLAN_ASN_RUNSHEET_NO],
		[PART_NO],
		[PART_CNAME],
		[BARCODE_DATA],
		[INHOUSE_PACKAGE_MODEL],
		[REQUIRED_INBOUND_PACKAGE_QTY],
		[CREATE_DATE],
		[SUPPLIER_NUM]
	)
	SELECT
		[PLAN_ASN_RUNSHEET_NO],
		[PART_NO],
		[PART_CNAME],
		[BARCODE_DATA],
		[INHOUSE_PACKAGE_MODEL],
		[REQUIRED_INBOUND_PACKAGE_QTY],
		[CREATE_DATE],
		[SUPPLIER_NUM]
	FROM [LES].[TT_SPM_DELIVERY_RUNSHEET_BARCODE] WITH (NOLOCK)
	WHERE [PLAN_ASN_RUNSHEET_NO] = @TWD_RUNSHEET_NO AND [IS_PRINTED] = 0

	SET NOCOUNT OFF
END