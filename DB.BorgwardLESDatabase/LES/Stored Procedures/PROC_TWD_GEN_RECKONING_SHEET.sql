

/********************************************************************/
/*   Project Name:  TWD						                         */
/*   Program Name:  [PROC_TWD_GEN_RECKONING_SHEET]                           */
/*   Called By:     界面                                 */
/*    Author:        SHENJINKUI                                  */
/********************************************************************/
CREATE PROCEDURE [LES].[PROC_TWD_GEN_RECKONING_SHEET]
(
	@PLANT			NVARCHAR(30),
	@CREATE_DATE			DATETIME,
	@SUPPLIER_NUM			NVARCHAR(8),
	@CREATE_USER NVARCHAR(100)
)
AS
--创建临时表
CREATE TABLE #TEMP_TWD_RECEIVED_GOODS_RECKONING(
	[INTERFACE_ID] [int] IDENTITY(1,1) NOT NULL,
	[PLANT] [nvarchar](5) NOT NULL,
	[ASN_NO] [nvarchar](50) NULL,
	[INTERFACE_STATUS] [int] NULL,
	[PART_NO] [nvarchar](20) NOT NULL,
	[PART_CNAME] [nvarchar](100) NULL,
	[PART_ENAME] [nvarchar](100) NULL,
	[SUPPLIER_NUM] [nvarchar](12) NULL,
	[INTERFACE_TYPE] [nvarchar](40) NULL,
	[MEASURING_UNIT_NO] [nvarchar](1) NULL,
	[REQUIRED_INBOUND_PACKAGE_QTY] [int]  NULL,
	[WAREHOUSE_NAME] [varchar](30) NULL,
	[RECEIVE_QTY] [int] NULL,
	[WMSID] [nvarchar](max) NULL,
	[ORDER_NO] [nvarchar](10) NULL,
	[ITEM_NO] [nvarchar](10) NULL,
	[COMMENTS] [nvarchar](200) NULL,
	[UPDATE_DATE] [datetime] NULL,
	[UPDATE_USER] [nvarchar](50) NULL,
	[CREATE_DATE] [datetime] NULL,
	[CREATE_USER] [nvarchar](50) NULL,
	[WMSID_SET] [nvarchar] (max) NULL,
	[RUNSHEET_NO_SET] [nvarchar](max) NULL	
	)
	
--把要结算的零件导入到临时表
INSERT INTO #TEMP_TWD_RECEIVED_GOODS_RECKONING
 SELECT 
 PLANT,
 NULL,
 NULL,
 PART_NO,
 MAX(ISNULL(PART_CNAME,'')),
 MAX(ISNULL(PART_ENAME,'')),
 SUPPLIER_NUM,
 NULL,
 MAX(ISNULL(MEASURING_UNIT_NO,'')),
 MAX(ISNULL(REQUIRED_INBOUND_PACKAGE_QTY,0)),
 ISNULL(WAREHOUSE,''),
 SUM(ISNULL(RECEIVE_QTY,0)) ,
 MAX(ISNULL(WMSID,0)),
 ORDER_NO,
 ITEM_NO,
 NULL,
 NULL,
 NULL,
 GETDATE(),
 NULL,
 LES.Func_Get_TWD_WMSID(PLANT, SUPPLIER_NUM, ORDER_NO, ITEM_NO, WAREHOUSE, PART_NO, MAX(CREATE_DATE)),
 LES.Func_Get_TWD_RUNSHEET_NO(PLANT, SUPPLIER_NUM, ORDER_NO, ITEM_NO, WAREHOUSE, PART_NO, MAX(CREATE_DATE))
 FROM [LES].[TI_TWD_RECEIVED_GOODS]
 WHERE CREATE_DATE<=@CREATE_DATE
 AND INTERFACE_TYPE='TWD'
 AND INTERFACE_STATUS IS NULL
 AND PLANT=@PLANT
 AND SUPPLIER_NUM=@SUPPLIER_NUM
 GROUP BY PLANT,SUPPLIER_NUM,ORDER_NO,ITEM_NO,WAREHOUSE,PART_NO

DECLARE @INTERFACE_ID int
DECLARE @Sequence int
DECLARE @SUPPLIER_TRANS nvarchar(100)
DECLARE @ORDER_NO nvarchar(10)
SET @INTERFACE_ID=0
DECLARE	@result int = 0
DECLARE @WAREHOUSE nvarchar(40)

set xact_abort on
begin try
Begin Transaction 

--判断是否还有未结算的明细	
    SET @INTERFACE_ID=-1
	SELECT @INTERFACE_ID=INTERFACE_ID FROM #TEMP_TWD_RECEIVED_GOODS_RECKONING WHERE COMMENTS  IS NULL
	SELECT @SUPPLIER_TRANS = PARAMETER_VALUE FROM LES.TS_SYS_CONFIG WHERE PARAMETER_NAME = 'SUPPLIER_TRANS'

--循环临时表，10个零件生成一张送货单
WHILE(@INTERFACE_ID <>-1)
BEGIN
	--print @result
	EXEC  @Sequence = [LES].[PROC_JIS_GET_NEXT_SEQUENCE] 'JIS_RECKONING_SHEET'
	
	--插入最大10条明细, 由于不同订单不能在一个送货单上，有的送货单会有不足10条的记录
	SELECT TOP 1 @ORDER_NO = ORDER_NO,@WAREHOUSE=ISNULL(WAREHOUSE_NAME,'')   FROM #TEMP_TWD_RECEIVED_GOODS_RECKONING WHERE COMMENTS  IS NULL ORDER BY INTERFACE_ID

	
--插入结算主表
	INSERT	INTO LES.TT_JIS_RECKONING_SHEET
	(
		RECKONING_SN,
		RECKONING_NO,
		DELIVERY_ORDER,
		ORDER_NO,
		RECEIVE_LOCATION,
		RECEIVED_DATE,
		SUPPLIER_NUM,
		SUPPLIER_TRANS,
		WMS_SEND_STATUS,
		TRANS_TYPE,
		WMS_SEND_TIME,
		UNLOAD_PLACE,
		LOAD_PLACE,
		PULL_TYPE,
		PULL_DETAIL,
		SUPPLY_STATUS,
		SUPPLY_CONFIRM_DATE,
		FIRST_CONFIRM_STATUS,
		FIRST_CONFIRM_DATE,
		SECOND_CONFIRM_STATUS,
		SECOND_CONFIRM_DATE,
		COMMENTS,
		UPDATE_DATE,
		UPDATE_USER,
		CREATE_DATE,
		CREATE_USER,
		[WHAREHOUSE]
	)
VALUES(
			@Sequence,
			@PLANT + RIGHT(CONVERT(varchar(4) , getdate(), 112 ),2) +@SUPPLIER_NUM+ SUBSTRING('000000' + CONVERT(NVARCHAR, @Sequence), LEN(CONVERT(NVARCHAR, @Sequence))+1 , 6) ,
			NULL, 
			@ORDER_NO,
			NULL,
			NULL,
			@SUPPLIER_NUM,
			@SUPPLIER_TRANS,
			0,--Bug5131初始状态由1改为0
			'TWD',
			NULL,
			NULL,
			NULL,
			2,
			NULL,
			NULL,
			NULL,
			NULL,
			NULL,
			NULL,
			NULL,
			NULL,
			NULL,
			NULL,			
			GETDATE(),
			@CREATE_USER,@WAREHOUSE)

--更新10条明细已结算
	if @ORDER_NO is null
	begin
		INSERT	INTO LES.TT_JIS_RECKONING_SHEET_DETAIL
		SELECT	TOP 10 @Sequence, T.PLANT, NULL, NULL, T.PART_NO, T.ITEM_NO, NULL, '',
				'', T.SUPPLIER_NUM, T.PART_CNAME, '', NULL, '', NULL, NULL, T.MEASURING_UNIT_NO,
				T.PART_CNAME, T.CREATE_DATE, T.RUNSHEET_NO_SET, T.REQUIRED_INBOUND_PACKAGE_QTY, T.RECEIVE_QTY, T.WMSID_SET, T.COMMENTS, T.WAREHOUSE_NAME,T.ORDER_NO
		FROM	#TEMP_TWD_RECEIVED_GOODS_RECKONING T
		WHERE	T.COMMENTS  IS NULL
		AND		T.ORDER_NO is null
		AND     ISNULL(T.WAREHOUSE_NAME,'')=@WAREHOUSE 
		ORDER BY T.INTERFACE_ID

		UPDATE	#TEMP_TWD_RECEIVED_GOODS_RECKONING SET COMMENTS = 'TEMP'
		WHERE INTERFACE_ID IN(SELECT TOP 10 INTERFACE_ID  FROM #TEMP_TWD_RECEIVED_GOODS_RECKONING
		WHERE	COMMENTS  IS NULL
		AND		ORDER_NO is null
		AND     ISNULL(WAREHOUSE_NAME,'')=@WAREHOUSE 
		ORDER BY INTERFACE_ID	
	)
	end
	else
	begin
		INSERT	INTO LES.TT_JIS_RECKONING_SHEET_DETAIL
		SELECT	TOP 10 @Sequence, T.PLANT, NULL, NULL, T.PART_NO, T.ITEM_NO, NULL, '',
				'', T.SUPPLIER_NUM, T.PART_CNAME, '', NULL, '', NULL, NULL, T.MEASURING_UNIT_NO,
				T.PART_CNAME, T.CREATE_DATE, T.RUNSHEET_NO_SET, T.REQUIRED_INBOUND_PACKAGE_QTY, T.RECEIVE_QTY, T.WMSID_SET, T.COMMENTS, T.WAREHOUSE_NAME,T.ORDER_NO
		FROM	#TEMP_TWD_RECEIVED_GOODS_RECKONING T
		WHERE	T.COMMENTS  IS NULL
		AND		T.ORDER_NO = @ORDER_NO
		AND     ISNULL(WAREHOUSE_NAME,'')=@WAREHOUSE 
		ORDER BY T.INTERFACE_ID
		
		UPDATE	#TEMP_TWD_RECEIVED_GOODS_RECKONING SET COMMENTS = 'TEMP'
		WHERE INTERFACE_ID IN(SELECT TOP 10 INTERFACE_ID  FROM #TEMP_TWD_RECEIVED_GOODS_RECKONING
		WHERE	COMMENTS  IS NULL
		AND		ORDER_NO = @order_no
		AND     ISNULL(WAREHOUSE_NAME,'')=@WAREHOUSE 
		ORDER BY INTERFACE_ID)
	end
	
--判断是否还有未结算的明细	
    SET @INTERFACE_ID=-1
	SELECT @INTERFACE_ID=INTERFACE_ID FROM #TEMP_TWD_RECEIVED_GOODS_RECKONING WHERE COMMENTS  IS NULL
	SET @result=@result+1
	
	
END

--更新收货的状态
UPDATE [LES].[TI_TWD_RECEIVED_GOODS] SET INTERFACE_STATUS=1
 WHERE CREATE_DATE<=@CREATE_DATE
 AND INTERFACE_TYPE='TWD'
 AND INTERFACE_STATUS IS NULL
 AND SUPPLIER_NUM=@SUPPLIER_NUM
 and PLANT=@PLANT
 
commit transaction
end try

begin catch
--出错，则返回执行不成功，回滚事务
rollback transaction
print error_message()
end catch

--释放临时表
DROP TABLE #TEMP_TWD_RECEIVED_GOODS_RECKONING

SELECT @result