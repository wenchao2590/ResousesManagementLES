/************************************************************************/
/*   Project Name:  TWD													*/
/*   Program Name:  [LES].[PROC_TWD_UPDATE_COUNTER_INSERT_CONSUME] 		*/
/*   Called By:     procedure											*/
/*   Author:        孙述霄												*/
/*   Date:			2017-05-19											*/
/*   Note:			更新TWD计数器										*/
/************************************************************************/
CREATE PROCEDURE [LES].[PROC_TWD_UPDATE_COUNTER_INSERT_CONSUME]
(
	@plant NVARCHAR(5),
	@assemblyLine NVARCHAR(10),
	@boxPart NVARCHAR(10),
	@supplier NVARCHAR(12),
	@dock NVARCHAR(10)
)
AS
BEGIN
	SET NOCOUNT ON
	--插入消耗-针对没有托盘的数据
	BEGIN TRY
		--清空本次拉动数量
		UPDATE [LES].[TT_TWD_CONSUME_COUNTER] WITH (ROWLOCK) SET CURRENT_PULL_COUNT = 0

		--根据拉动模式，计算本次拉动数量
		--pull_mode ：1 向上圆整；2 向下圆整；3 按消耗拉动；4 MIN/MAX拉动 
		--默认1 向上圆整，数据为空时，也为向上圆整
		UPDATE [LES].[TT_TWD_CONSUME_COUNTER] WITH (ROWLOCK)
		SET [CURRENT_PULL_COUNT] = [LES].[FUNC_TWD_GET_FUTURE_CEILING] ([CURRENT_PART_COUNT], [INBOUND_PACKAGE], [PLANT], [ASSEMBLY_LINE], [PART_NO], [INBOUND_PART_CLASS]) 
		WHERE ([PULL_MODE] = 1 OR [PULL_MODE] IS NULL) AND [CURRENT_PART_COUNT] > 0
		AND [PLANT] = @plant AND [ASSEMBLY_LINE] = @assemblyLine 
		AND [INBOUND_PART_CLASS] = @boxPart AND [SUPPLIER_NUM] = @supplier
		--最小拉动箱数
		UPDATE [LES].[TT_TWD_CONSUME_COUNTER] WITH (ROWLOCK)
		SET [CURRENT_PULL_COUNT] = ((CONVERT(INT, [CURRENT_PULL_COUNT])/ISNULL([INBOUND_PACKAGE], 99999)) / [MIN_PULL_BOX]) * [MIN_PULL_BOX] * ISNULL([INBOUND_PACKAGE], 99999)
		WHERE ([PULL_MODE] = 1 OR [PULL_MODE] IS NULL)
		AND [PLANT] = @plant AND [ASSEMBLY_LINE] = @assemblyLine 
		AND [INBOUND_PART_CLASS] = @boxPart AND [SUPPLIER_NUM] = @supplier
		AND [CURRENT_PULL_COUNT] > 0 AND [MIN_PULL_BOX] > 0
		--批量拉动箱数
		UPDATE [LES].[TT_TWD_CONSUME_COUNTER] WITH (ROWLOCK)
		SET [CURRENT_PULL_COUNT] = CEILING(CONVERT(DECIMAL, (CONVERT(INT, [CURRENT_PULL_COUNT])/ISNULL([INBOUND_PACKAGE], 99999))) / [BATCH_PULL_BOX]) * [BATCH_PULL_BOX] * ISNULL([INBOUND_PACKAGE], 99999)
		WHERE ([PULL_MODE] = 1 OR [PULL_MODE] IS NULL)
		AND [PLANT] = @plant AND [ASSEMBLY_LINE] = @assemblyLine 
		AND [INBOUND_PART_CLASS] = @boxPart AND [SUPPLIER_NUM] = @supplier
		AND [CURRENT_PULL_COUNT] > 0 AND [BATCH_PULL_BOX] > 0

		--2 向下圆整
		UPDATE [LES].[TT_TWD_CONSUME_COUNTER] WITH (ROWLOCK)
		SET [CURRENT_PULL_COUNT] = CONVERT(INT, [CURRENT_PART_COUNT])/ISNULL([INBOUND_PACKAGE], 99999) * ISNULL([INBOUND_PACKAGE], 99999)
		WHERE [PULL_MODE] = 2 AND [CURRENT_PART_COUNT] > [INBOUND_PACKAGE]
		AND [PLANT] = @plant AND [ASSEMBLY_LINE] = @assemblyLine 
		AND [INBOUND_PART_CLASS] = @boxPart AND [SUPPLIER_NUM] = @supplier
		--最小拉动箱数
		UPDATE [LES].[TT_TWD_CONSUME_COUNTER] WITH (ROWLOCK)
		SET [CURRENT_PULL_COUNT] = ((CONVERT(INT, [CURRENT_PULL_COUNT])/ISNULL([INBOUND_PACKAGE], 99999)) / [MIN_PULL_BOX]) * [MIN_PULL_BOX] * ISNULL([INBOUND_PACKAGE], 99999)
		WHERE [PULL_MODE] = 2
		AND [PLANT] = @plant AND [ASSEMBLY_LINE] = @assemblyLine 
		AND [INBOUND_PART_CLASS] = @boxPart AND [SUPPLIER_NUM] = @supplier
		AND [CURRENT_PULL_COUNT] > 0 AND [MIN_PULL_BOX] > 0
		--批量拉动箱数
		UPDATE [LES].[TT_TWD_CONSUME_COUNTER] WITH (ROWLOCK)
		SET [CURRENT_PULL_COUNT] = CEILING(CONVERT(DECIMAL, (CONVERT(INT, [CURRENT_PULL_COUNT])/ISNULL([INBOUND_PACKAGE], 99999))) / [BATCH_PULL_BOX]) * [BATCH_PULL_BOX] * ISNULL([INBOUND_PACKAGE], 99999)
		WHERE [PULL_MODE] = 2
		AND [PLANT] = @plant AND [ASSEMBLY_LINE] = @assemblyLine 
		AND [INBOUND_PART_CLASS] = @boxPart AND [SUPPLIER_NUM] = @supplier
		AND [CURRENT_PULL_COUNT] > 0 AND [BATCH_PULL_BOX] > 0

		--3 按消耗拉动
		UPDATE [LES].[TT_TWD_CONSUME_COUNTER] WITH (ROWLOCK)
		SET [CURRENT_PULL_COUNT] = [CURRENT_PART_COUNT]
		WHERE [PULL_MODE] = 3 AND [CURRENT_PART_COUNT] > 0
		AND [PLANT] = @plant AND [ASSEMBLY_LINE] = @assemblyLine 
		AND [INBOUND_PART_CLASS] = @boxPart AND [SUPPLIER_NUM] = @supplier

		--4 MIN/MAX拉动（需要按照实际库存拉动，不能按照计数器来拉动）
		UPDATE A	
		SET A.[CURRENT_PULL_COUNT] = CEILING(CONVERT(NUMERIC(18,2), (A.[MAX] - (D.[STOCKS_NUM] + ISNULL(G.[REQUIRED_QTY],0))))/ISNULL(A.[INBOUND_PACKAGE],99999)) * ISNULL(A.[INBOUND_PACKAGE],99999) 
		FROM [LES].[TT_TWD_CONSUME_COUNTER] A WITH (ROWLOCK)
		INNER JOIN
		(
			--找到对应零件类在拉动仓储表的信息，并且根据对应的库存信息更新拉动数量
			SELECT
				SUM(C.[STOCKS_NUM]) AS [STOCKS_NUM],
				B.[PLANT],
				B.[ASSEMBLY_LINE],
				B.[INHOUSE_PART_CLASS],
				B.[PART_NO]
			FROM [LES].[TM_BAS_MAINTAIN_INHOUSE_LOGISTIC_STANDARD] B WITH (NOLOCK)
			INNER JOIN [LES].[TT_WMS_STOCKS] C WITH (NOLOCK) ON C.[PLANT] = B.[PLANT] AND C.[WM_NO] = B.[WAREHOUSE] AND C.[ZONE_NO] = B.[PLANT_ZONE] AND C.[PART_NO] = B.[PART_NO]
			WHERE B.[INHOUSE_SYSTEM_MODE] = 'TWD'
			GROUP BY B.[PLANT], B.[ASSEMBLY_LINE], B.[INHOUSE_PART_CLASS], B.[PART_NO]
		) D ON D.[PLANT] = A.[PLANT] AND D.[ASSEMBLY_LINE] = A.[ASSEMBLY_LINE] AND D.[INHOUSE_PART_CLASS] = A.[INBOUND_PART_CLASS] AND D.[PART_NO] = A.[PART_NO]
		LEFT JOIN
		(
			--考虑在途库存
			SELECT
				SUM(ISNULL(F.[REQUIRED_INBOUND_PACKAGE_QTY], 0) - ISNULL(F.[ACTUAL_INBOUND_PACKAGE_QTY], 0)) AS [REQUIRED_QTY],
				E.[PLANT],
				E.[ASSEMBLY_LINE],
				F.[PART_NO],
				E.[SUPPLIER_NUM]
			FROM [LES].[TT_TWD_RUNSHEET] E WITH (NOLOCK)
			INNER JOIN [LES].[TT_TWD_RUNSHEET_DETAIL] F WITH (NOLOCK)
			ON E.[TWD_RUNSHEET_SN] = F.[TWD_RUNSHEET_SN]
			WHERE E.[RUNSHEET_TYPE] <> 3 AND E.[SHEET_STATUS] = 0 AND E.[PLANT] = @plant	--正常及紧急拉动单，且未关闭
			AND E.[ASSEMBLY_LINE] = @assemblyLine AND E.[SUPPLIER_NUM] = @supplier AND E.[BOX_PARTS] = @boxPart
			GROUP BY E.[PLANT], E.[ASSEMBLY_LINE], F.[PART_NO], E.[SUPPLIER_NUM]
		) G ON G.[PLANT] = A.[PLANT] AND G.[ASSEMBLY_LINE] = A.[ASSEMBLY_LINE] AND G.[PART_NO] = A.[PART_NO] AND G.[SUPPLIER_NUM] = A.[SUPPLIER_NUM]
		WHERE A.[PULL_MODE] = 4 AND ((D.[STOCKS_NUM] + ISNULL(G.[REQUIRED_QTY],0)) <= A.[MIN]) AND ((D.[STOCKS_NUM] + ISNULL(G.[REQUIRED_QTY],0)) < A.[MAX])
		AND A.[PLANT] = @plant AND A.[ASSEMBLY_LINE] = @assemblyLine 
		AND A.[INBOUND_PART_CLASS] = @boxPart AND A.[SUPPLIER_NUM] = @supplier
		--最小拉动箱数
		UPDATE [LES].[TT_TWD_CONSUME_COUNTER] WITH (ROWLOCK)
		SET [CURRENT_PULL_COUNT] = ((CONVERT(INT, [CURRENT_PULL_COUNT])/ISNULL([INBOUND_PACKAGE], 99999)) / [MIN_PULL_BOX]) * [MIN_PULL_BOX] * ISNULL([INBOUND_PACKAGE], 99999)
		WHERE [PULL_MODE] = 4
		AND [PLANT] = @plant AND [ASSEMBLY_LINE] = @assemblyLine 
		AND [INBOUND_PART_CLASS] = @boxPart AND [SUPPLIER_NUM] = @supplier
		AND [CURRENT_PULL_COUNT] > 0 AND [MIN_PULL_BOX] > 0
		--批量拉动箱数
		UPDATE [LES].[TT_TWD_CONSUME_COUNTER] WITH (ROWLOCK)
		SET [CURRENT_PULL_COUNT] = CEILING(CONVERT(DECIMAL, (CONVERT(INT, [CURRENT_PULL_COUNT])/ISNULL([INBOUND_PACKAGE], 99999))) / [BATCH_PULL_BOX]) * [BATCH_PULL_BOX] * ISNULL([INBOUND_PACKAGE], 99999)
		WHERE [PULL_MODE] = 4
		AND [PLANT] = @plant AND [ASSEMBLY_LINE] = @assemblyLine 
		AND [INBOUND_PART_CLASS] = @boxPart AND [SUPPLIER_NUM] = @supplier
		AND [CURRENT_PULL_COUNT] > 0 AND [BATCH_PULL_BOX] > 0

		INSERT INTO [LES].[TI_TWD_MATERIAL_CONSUME]
		(
			[PLANT_ZONE],
			[WORKSHOP],
			[ASSEMBLY_LINE],
			[PLANT],
			[LOCATION],
			[REQUEST_TIME],
			[INTERFACE_STATUS],
			[PROCESS_TIME],
			[PART_NO],
			[INDENTIFY_PART_NO],
			[PART_CNAME],
			[PART_ENAME],
			[SUPPLIER_NUM],
			[DOCK],
			[BOX_PARTS],
			[INTERFACE_TYPE],
			[PACK_COUNT],
			[INBOUND_PACKAGE_MODEL],
			[INBOUND_PACKAGE],
			[MEASURING_UNIT_NO],
			--,[EXPECTED_ARRIVAL_TIME]
			--,[RDC_DLOC]
			--,[PICKUP_SEQ_NO]
			--,[SEQUENCE_NO]
			[IS_ORGANIZE_SHEET],
			--,[SEND_STATUS]
			--,[SEND_TIME]
			--,[IS_CANCEL]
			--,[UPDATE_DATE]
			--,[UPDATE_USER]
			[CREATE_DATE],
			[CREATE_USER],
			--,[COMMENTS]
			[INHOUSE_PACKAGE_MODEL]
		)
		SELECT 
			C.[PLANT_ZONE],
			C.[WORKSHOP],
			C.[ASSEMBLY_LINE],
			C.[PLANT],
			NULL,
			GETDATE(),
			6,
			NULL,
			[PART_NO] ,
			[INDENTIFY_PART_NO] ,
			[PART_CNAME] ,
			[PART_ENAME] ,
			@supplier,
			@dock, --DOCK
			[INBOUND_PART_CLASS] ,
			0,--[INTERFACE_TYPE]
			C.[CURRENT_PULL_COUNT],
			[INBOUND_PACKAGE_MODEL] ,
			ISNULL([INBOUND_PACKAGE],99999) ,
			[MEASURING_UNIT_NO],
			--null,
			--null,
			--null,
			--null,
			2,
			--0,
			--null,
			--0,
			--null,
			--null,
			GETDATE(),
			'twd consume'
			--''
			,NULL
		FROM [LES].[TT_TWD_CONSUME_COUNTER] C WITH (NOLOCK)
		INNER JOIN [LES].[TM_TWD_BOX_PARTS] B WITH (NOLOCK)
		ON C.[PLANT] = B.[PLANT] AND C.[ASSEMBLY_LINE] = B.[ASSEMBLY_LINE] AND C.[INBOUND_PART_CLASS] = B.[BOX_PARTS]
 		WHERE C.[PLANT] = @plant 
		AND C.[ASSEMBLY_LINE] = @assemblyLine 
		AND C.[CURRENT_PULL_COUNT] > 0
		--AND (C.[INHOUSE_PACKAGE_MODEL] IS NULL OR LEN(C.[INHOUSE_PACKAGE_MODEL]) < 1)
		AND C.[INBOUND_PART_CLASS] = @boxPart AND C.[SUPPLIER_NUM] = @supplier
		--AND ISNULL(C.PALLET_PACKAGE,0) = 0

	 
		 
		----更新计数器--针对没有托盘的数据
		--update [LES].[TT_TWD_CONSUME_COUNTER] 
		--	set CURRENT_PART_COUNT = CURRENT_PART_COUNT -convert(numeric(18,2), ceiling( convert(numeric(18,2),[CURRENT_PART_COUNT])/[INBOUND_PACKAGE]) * [INBOUND_PACKAGE])
		--	from [LES].[TT_TWD_CONSUME_COUNTER] c inner join LES.TM_TWD_BOX_PARTS b
		--	on c.PLANT = b.PLANT and c.ASSEMBLY_LINE = b.ASSEMBLY_LINE and c.INBOUND_PART_CLASS = b.BOX_PARTS
 		--	 where c.PLANT = @plant 
		--		 and c.ASSEMBLY_LINE  = @assemblyLine 
		--		 and CURRENT_PART_COUNT >0 AND (C.INHOUSE_PACKAGE_MODEL IS NULL OR LEN(C.INHOUSE_PACKAGE_MODEL) < 1)
		--		 and INBOUND_PART_CLASS = @boxPart   and C.SUPPLIER_NUM=@supplier
		--		  and isnull(c.PALLET_PACKAGE,0)=0

		--更新计数器--针对没有托盘的数据(根据本次拉动数量来扣减计数器）
		UPDATE [LES].[TT_TWD_CONSUME_COUNTER] WITH (ROWLOCK)
		SET [CURRENT_PART_COUNT] = [CURRENT_PART_COUNT] - [CURRENT_PULL_COUNT]
		WHERE [PLANT] = @plant 
		AND [ASSEMBLY_LINE]  = @assemblyLine 
		AND [CURRENT_PART_COUNT] >0 --AND (INHOUSE_PACKAGE_MODEL IS NULL OR LEN(INHOUSE_PACKAGE_MODEL) < 1)
		AND [INBOUND_PART_CLASS] = @boxPart AND [SUPPLIER_NUM] = @supplier
		AND [PULL_MODE] <> 4
	END TRY
	BEGIN CATCH
		--记录错误信息
		INSERT INTO [LES].[TS_SYS_EXCEPTION] ([TIME_STAMP], [APPLICATION], [METHOD], [CLASS],  [EXCEPTION_MESSAGE], [ERROR_CODE])
		SELECT GETDATE(), 'TWD', '[LES].[PROC_TWD_UPDATE_COUNTER_INSERT_CONSUME]', 'Procedure' ,ERROR_MESSAGE() ,ERROR_LINE()
	END CATCH
END