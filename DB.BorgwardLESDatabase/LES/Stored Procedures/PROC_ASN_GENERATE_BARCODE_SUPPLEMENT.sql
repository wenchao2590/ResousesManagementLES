/********************************************************************/
/*   Project Name:  ASN												*/
/*   Program Name:  [LES].[PROC_ASN_GENERATE_BARCODE_SUPPLEMENT]	*/
/*   Called By:     web page										*/
/*   Author:        孙述霄											*/
/*   Create date:	2017-10-16										*/
/*   Note:			ASN 生成补打条码								*/
/********************************************************************/
CREATE PROCEDURE [LES].[PROC_ASN_GENERATE_BARCODE_SUPPLEMENT]
(
	@RunsheetSN INT,
	@RunsheetDetailId INT,
	@PrintQty INT
)
AS
BEGIN
	SET NOCOUNT ON
	DECLARE @PLANT NVARCHAR(5)
	DECLARE @ASSEMBLYLINE NVARCHAR(10)
	DECLARE @WM_NO NVARCHAR(10)
	DECLARE @ZONE_NO NVARCHAR(20)
	DECLARE @ASN_NO NVARCHAR(50)
	DECLARE @DOCK NVARCHAR(10)
	DECLARE @REQUIRED_DATE DATETIME
	DECLARE @DLOC NVARCHAR(20)
	DECLARE @PART_NO NVARCHAR(20)
	DECLARE @PART_CNAME NVARCHAR(100)
	DECLARE @PART_NICKNAME NVARCHAR(50)
	DECLARE @INHOUSE_PACKAGE_MODEL NVARCHAR(30)
	DECLARE @INBOUND_PACKAGE INT
	DECLARE @SUPPLIER_NUM NVARCHAR(12)
	DECLARE @SUPPLIER_NAME NVARCHAR(100)
	DECLARE @BOX_PARTS NVARCHAR(10)
	DECLARE @TWD_RUNSHEET_NO NVARCHAR(36)
	DECLARE @MEASURING_UNIT_NO NVARCHAR(8)
	DECLARE @LINE_POSITION NVARCHAR(100)
	DECLARE @INNER_LOCATION NVARCHAR(50)
		
	SELECT
		@PLANT = A.[PLANT],
		@ASSEMBLYLINE = A.[ASSEMBLY_LINE],
		@WM_NO = B.[WM_NO],
		@ZONE_NO = B.[ZONE_NO],
		@ASN_NO = A.[ASN_NO],
		@DOCK = A.[DOCK],
		@REQUIRED_DATE = A.[PROMISE_DELIVERY_TIME]
    FROM [LES].[TT_SPM_RUNSHEET_ASN] A WITH (NOLOCK)
	LEFT JOIN [LES].[TM_WMM_ZONES] AS B WITH (NOLOCK) ON B.[PLANT] = A.[PLANT] AND B.[ZONE_NO] = A.[PLANT_ZONE]
	WHERE A.[ID] = @RunsheetSN

	SELECT
		@PART_NO = D.[PART_NO],
		@PART_CNAME = D.[PART_CNAME],
		@INHOUSE_PACKAGE_MODEL = D.[INBOUND_PACKAGE_MODEL],
		@INBOUND_PACKAGE = D.[INBOUND_PACKAGE],
		@SUPPLIER_NUM = D.[SUPPLIER_NUM],
		@SUPPLIER_NAME = S.[SUPPLIER_NAME],
		@BOX_PARTS = D.[BOX_PARTS],
		@TWD_RUNSHEET_NO = D.[TWD_RUNSHEET_NO]
	FROM [LES].[TT_SPM_RUNSHEET_ASN_DETAIL] D WITH (NOLOCK)
	LEFT JOIN [LES].[TM_BAS_SUPPLIER] S WITH (NOLOCK) ON D.[SUPPLIER_NUM] = S.[SUPPLIER_NUM]
	WHERE [ID] = @RunsheetDetailId

	IF @ZONE_NO = '2016'
		BEGIN
			--获取库位地址
			SELECT TOP 1 @DLOC = [DLOC] FROM [LES].[TM_BAS_PARTS_STOCK] WITH (NOLOCK) WHERE  [PLANT] = @PLANT AND [ZONE_NO] = '2015' AND [PART_NO] = @PART_NO
			--获取超市地址
			SELECT TOP 1 @INNER_LOCATION = [DLOC] FROM [LES].[TM_BAS_PARTS_STOCK] WITH (NOLOCK) WHERE  [PLANT] = @PLANT AND [ZONE_NO] = '2016' AND [PART_NO] = @PART_NO
		END
	ELSE
		BEGIN
			--获取库位地址
			SELECT TOP 1 @DLOC = [DLOC] FROM [LES].[TM_BAS_PARTS_STOCK] WITH (NOLOCK) WHERE  [PLANT] = @PLANT AND [WM_NO] = @WM_NO AND [ZONE_NO] = @ZONE_NO AND [PART_NO] = @PART_NO
			--获取超市地址
			SELECT TOP 1 @INNER_LOCATION = [DLOC] FROM [LES].[TM_BAS_PARTS_STOCK] WITH (NOLOCK) WHERE  [PLANT] = @PLANT AND [PART_NO] = @PART_NO AND [WM_NO] = @WM_NO AND [ZONE_NO] IN
			(
				SELECT [REPACKAGE_ZONE] FROM [LES].[TM_WMM_ZONES] WITH (NOLOCK) WHERE [PLANT] = @PLANT AND [WM_NO] = @WM_NO AND [ZONE_NO] = @ZONE_NO
			)
		END

	--获取单位、昵称
	SELECT
		@MEASURING_UNIT_NO = [PART_UNITS]
	FROM [LES].[TM_BAS_MAINTAIN_PARTS] WITH (NOLOCK)
	WHERE [PART_NO] = @PART_NO
	--获取昵称
	SELECT
		@PART_NICKNAME = [PART_NICKNAME]
	FROM [LES].[TM_BAS_PARTS_STOCK] WITH (NOLOCK)
	WHERE [PLANT] = @PLANT AND [WM_NO] = @WM_NO AND [ZONE_NO] = @ZONE_NO AND [PART_NO] = @PART_NO

	--从拉动关系中获取线边地址
	SELECT TOP 1
		@LINE_POSITION = L.[STORAGE_LOCATION] 
	FROM [LES].[TM_BAS_PARTS_STOCK] S WITH (NOLOCK) 
	INNER JOIN [LES].[TM_BAS_MAINTAIN_INHOUSE_LOGISTIC_STANDARD] L WITH (NOLOCK)
	ON L.[PLANT] = S.[PLANT] AND L.[PART_NO] = S.[PART_NO] and L.[ASSEMBLY_LINE] = S.[ASSEMBLY_LINE]
	WHERE S.[PLANT] = @PLANT AND S.[PART_NO] = @PART_NO AND S.[ZONE_NO] = ISNULL(@ZONE_NO, '') AND ISNULL(L.[STORAGE_LOCATION], '') <> ''
	IF	(SELECT COUNT(1) FROM 
		[LES].[TM_BAS_PARTS_STOCK] S WITH (NOLOCK) 
		INNER JOIN [LES].[TM_BAS_MAINTAIN_INHOUSE_LOGISTIC_STANDARD] L WITH (NOLOCK)
		ON L.[PLANT] = S.[PLANT] AND L.[PART_NO] = S.[PART_NO] AND L.[ASSEMBLY_LINE] = S.[ASSEMBLY_LINE]
		WHERE S.[PLANT] = @PLANT AND S.[PART_NO] = @PART_NO AND S.[ZONE_NO] = ISNULL(@ZONE_NO, '') AND ISNULL(L.[STORAGE_LOCATION], '') <> '')>1
	BEGIN
		SET @LINE_POSITION = @LINE_POSITION + '...'
	END

	WHILE(@PrintQty > 0)
		BEGIN
			DECLARE @BARCODENO NVARCHAR(32) 
			DECLARE @CREATEDON DATETIME
			EXEC LES.[PROC_SYS_GET_NEXT_BARCODE_SEQUENCE] 'BARCODE', @BARCODENO OUTPUT
			SET @CREATEDON = GETDATE()

			INSERT INTO [LES].[TT_SPM_DELIVERY_RUNSHEET_BARCODE]
			(
				[PLAN_ASN_RUNSHEET_NO],
				[PART_NO],
				[PART_CNAME],
				[BARCODE_DATA],
				[INHOUSE_PACKAGE_MODEL],
				[REQUIRED_INBOUND_PACKAGE_QTY],
				[CREATE_DATE],
				[SUPPLIER_NUM],
				[SUPPLIER_NAME],
				[BOX_PARTS],
				[MEASURING_UNIT_NO],
				[TWD_RUNSHEET_NO],
				[PLANT],
				[STORAGE_LOCATION],
				[INNER_LOCATION],
				[PART_NICKNAME],
				[LINE_POSITION],
				[REQUIRED_DATE],
				[DOCK],
				[PRINT_SUPPLEMENT]
			)
			VALUES
			(
				@ASN_NO,
				@PART_NO,
				@PART_CNAME,
				@BARCODENO,
				@INHOUSE_PACKAGE_MODEL,
				@INBOUND_PACKAGE,
				@CREATEDON,
				ISNULL(@SUPPLIER_NUM, ''),
				ISNULL(@SUPPLIER_NAME, ''),
				@BOX_PARTS,
				ISNULL(@MEASURING_UNIT_NO, ''),
				@TWD_RUNSHEET_NO,
				@PLANT,
				@DLOC,
				@INNER_LOCATION,
				@PART_NICKNAME,
				@LINE_POSITION,
				@REQUIRED_DATE,
				@DOCK,
				1
			)

			SET @PrintQty = @PrintQty - 1
		END

	--打印新的箱条码
	INSERT INTO [LES].[TT_WMS_AUTOPRINT_BARCODE]
	(
		[PLAN_ASN_RUNSHEET_NO],
		[PART_NO],
		[PART_CNAME],
		[BARCODE_DATA],
		[INHOUSE_PACKAGE_MODEL],
		[REQUIRED_INBOUND_PACKAGE_QTY],
		[CREATE_DATE],
		[SUPPLIER_NUM]
	)
	SELECT
		[PLAN_ASN_RUNSHEET_NO],
		[PART_NO],
		[PART_CNAME],
		[BARCODE_DATA],
		[INHOUSE_PACKAGE_MODEL],
		[REQUIRED_INBOUND_PACKAGE_QTY],
		[CREATE_DATE],
		[SUPPLIER_NUM]
	FROM [LES].[TT_SPM_DELIVERY_RUNSHEET_BARCODE] WITH (NOLOCK)
	WHERE [PLAN_ASN_RUNSHEET_NO] = @ASN_NO AND [IS_PRINTED] = 0

	SET NOCOUNT OFF
END