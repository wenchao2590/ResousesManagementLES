/********************************************************************/
/*   Project Name:  Foton LES System								*/
/*   Program Name:  [LES].[PROC_TWD_UPDATE_RECEIVE_ISCLOSE]			*/
/*   Called By:     by the Page										*/
/*   Purpose:       TWD拉动收货完成时更改相应拉动单数据			    */
/*   author:        孙述霄    2017-07-20							*/
/********************************************************************/
CREATE PROCEDURE [LES].[PROC_TWD_UPDATE_RECEIVE_ISCLOSE]
(
	@RECEIVE_ID INT,				--入库单ID号
	@RUNSHEET_CODE NVARCHAR(12),	--交易类别
	@LOGINAME NVARCHAR(50)			--处理人
)
AS
BEGIN
    BEGIN TRY    
        BEGIN TRANSACTION
			DECLARE @RECEIVE_TYPE INT
			IF @RUNSHEET_CODE = 'TWD' OR (@RUNSHEET_CODE = 'CTD' AND EXISTS (SELECT 1 FROM [LES].[TT_TWD_RUNSHEET] WITH (NOLOCK) WHERE [TWD_RUNSHEET_NO] = (SELECT [RECEIVE_NO] FROM [LES].[TT_WMM_RECEIVE] WITH (NOLOCK) WHERE [RECEIVE_ID] = @RECEIVE_ID)))
				BEGIN
					--更新TWD明细实收数量
					UPDATE B
					SET B.[ACTUAL_INBOUND_PACKAGE] = D.[ACTUAL_BOX_NUM],
						B.[ACTUAL_INBOUND_PACKAGE_QTY] = D.[ACTUAL_QTY]
					FROM [LES].[TT_TWD_RUNSHEET] A WITH (NOLOCK)
					INNER JOIN [LES].[TT_TWD_RUNSHEET_DETAIL] B WITH (ROWLOCK) ON A.[TWD_RUNSHEET_SN] = B.[TWD_RUNSHEET_SN]
					INNER JOIN [LES].[TT_WMM_RECEIVE] C WITH (NOLOCK) ON C.[RECEIVE_NO] = A.[TWD_RUNSHEET_NO]
					INNER JOIN [LES].[TT_WMM_RECEIVE_DETAIL] D WITH (NOLOCK) ON D.[RECEIVE_ID] = C.[RECEIVE_ID] AND D.[PART_NO] = B.[PART_NO]
					WHERE C.[RECEIVE_ID] = @RECEIVE_ID

					--更新TWD拉动单状态
					UPDATE [LES].[TT_TWD_RUNSHEET] WITH (ROWLOCK)
					SET [ACTUAL_ARRIVAL_TIME] = GETDATE(),
						[UNLOADING_TIME] = 0,
						[SHEET_STATUS] = 1,
						[UPDATE_DATE] = GETDATE(),
						[UPDATE_USER] = @LOGINAME
					WHERE TWD_RUNSHEET_NO IN (SELECT [RECEIVE_NO] FROM [LES].[TT_WMM_RECEIVE] WITH (NOLOCK) WHERE [RECEIVE_ID] = @RECEIVE_ID)

					--更新条码状态
					UPDATE [LES].[TT_SPM_DELIVERY_RUNSHEET_BARCODE] WITH (ROWLOCK)
					SET [IS_PRINTED] = 3,
						[UPDATE_DATE] = GETDATE(),
						[UPDATE_USER] = @LOGINAME
					WHERE [PLAN_ASN_RUNSHEET_NO] IN (SELECT [RECEIVE_NO] FROM [LES].[TT_WMM_RECEIVE] WITH (NOLOCK) WHERE [RECEIVE_ID] = @RECEIVE_ID)
				END

			IF @RUNSHEET_CODE = 'ASN' OR (@RUNSHEET_CODE = 'CTD' AND NOT EXISTS (SELECT 1 FROM [LES].[TT_TWD_RUNSHEET] WITH (NOLOCK) WHERE [TWD_RUNSHEET_NO] = (SELECT [RECEIVE_NO] FROM [LES].[TT_WMM_RECEIVE] WITH (NOLOCK) WHERE [RECEIVE_ID] = @RECEIVE_ID)))
				BEGIN
					--更新ASN明细实收数量
					UPDATE B
					SET B.[ACTUAL_INBOUND_PACKAGE] = D.[ACTUAL_BOX_NUM],
						B.[ACTUAL_INBOUND_PACKAGE_QTY] = D.[ACTUAL_QTY],
						B.[MODIFY_DATE] = GETDATE(),
						B.[MODIFY_USER] = @LOGINAME
					FROM [LES].[TT_SPM_RUNSHEET_ASN] A WITH (NOLOCK)
					INNER JOIN [LES].[TT_SPM_RUNSHEET_ASN_DETAIL] B WITH (ROWLOCK) ON A.[ID] = B.[ASN_ID]
					INNER JOIN [LES].[TT_WMM_RECEIVE] C WITH (NOLOCK) ON C.[RECEIVE_NO] = A.[ASN_NO]
					INNER JOIN [LES].[TT_WMM_RECEIVE_DETAIL] D WITH (NOLOCK) ON D.[RECEIVE_ID] = C.[RECEIVE_ID] AND D.[PART_NO] = B.[PART_NO] AND D.[TWD_RUNSHEET_NO] = B.[TWD_RUNSHEET_NO]
					WHERE C.[RECEIVE_ID] = @RECEIVE_ID

					--设置入库类型
					SET @RECEIVE_TYPE = 1
					IF EXISTS
					(
						SELECT
							1
						FROM [LES].[TT_SPM_RUNSHEET_ASN] A WITH (NOLOCK)
						INNER JOIN [LES].[TT_SPM_RUNSHEET_ASN_DETAIL] B WITH (ROWLOCK) ON A.[ID] = B.[ASN_ID]
						INNER JOIN [LES].[TT_WMM_RECEIVE] C WITH (NOLOCK) ON C.[RECEIVE_NO] = A.[ASN_NO]
						WHERE C.[RECEIVE_ID] = @RECEIVE_ID AND B.[REQUIRED_INBOUND_PACKAGE_QTY] <> B.[ACTUAL_INBOUND_PACKAGE_QTY]
					)
						BEGIN
							SET @RECEIVE_TYPE = 2
						END

					--更新ASN单据状态
					UPDATE [LES].[TT_SPM_RUNSHEET_ASN] WITH (ROWLOCK)
					SET [REAL_ARRIVAL_TIME] = GETDATE(),
						[STATUS] = 3,
						[RECEIVE_TYPE] = @RECEIVE_TYPE,
						[MODIFY_DATE] = GETDATE(),
						[MODIFY_USER] = @LOGINAME
					WHERE [ASN_NO] IN (SELECT [RECEIVE_NO] FROM [LES].[TT_WMM_RECEIVE] WITH (NOLOCK) WHERE [RECEIVE_ID] = @RECEIVE_ID)

					IF EXISTS (SELECT 1 FROM [LES].[TT_SPM_DELIVERY_RUNSHEET] A WITH (NOLOCK) WHERE [PLAN_RUNSHEET_NO] IN (SELECT [TWD_RUNSHEET_NO] FROM [LES].[TT_WMM_RECEIVE_DETAIL] WITH (NOLOCK) WHERE [RECEIVE_ID] = @RECEIVE_ID))
						BEGIN
							--更新交货单明细实收数量,确认数量
							UPDATE B
							SET B.[ACTUAL_INHOUSE_PACKAGE] = ISNULL(B.[ACTUAL_INHOUSE_PACKAGE], 0) + C.[ACTUAL_BOX_NUM],
								B.[ACTUAL_INHOUSE_PACKAGE_QTY] = ISNULL(B.[ACTUAL_INHOUSE_PACKAGE_QTY], 0) + C.[ACTUAL_QTY]
							FROM [LES].[TT_SPM_DELIVERY_RUNSHEET] A WITH (NOLOCK)
							INNER JOIN [LES].[TT_SPM_DELIVERY_RUNSHEET_DETAIL] B WITH (ROWLOCK) ON A.[PLAN_RUNSHEET_SN] = B.[PLAN_RUNSHEET_SN]
							INNER JOIN [LES].[TT_WMM_RECEIVE_DETAIL] C WITH (NOLOCK) ON C.[TWD_RUNSHEET_NO] = A.[PLAN_RUNSHEET_NO] AND C.[PART_NO] = B.[PART_NO]
							WHERE C.[RECEIVE_ID] = @RECEIVE_ID

							--更新交货单状态
							--草稿数=0，需求件数 - 草稿数 - 已确认数 - 实收件数 = 0，且交货单对应的ASN单都已经关闭，则关闭此交货单
							UPDATE A
							SET A.[SHEET_STATUS] = 10,
								A.[UPDATE_DATE] = GETDATE(),
								A.[UPDATE_USER] = @LOGINAME
							FROM [LES].[TT_SPM_DELIVERY_RUNSHEET] A WITH (ROWLOCK)
							WHERE A.[PLAN_RUNSHEET_NO] IN
							(
								SELECT DISTINCT [TWD_RUNSHEET_NO] FROM [LES].[TT_WMM_RECEIVE_DETAIL] WITH (NOLOCK) WHERE [RECEIVE_ID] = @RECEIVE_ID
							)
							AND NOT EXISTS
							(
								SELECT
									1
								FROM [LES].[TT_SPM_DELIVERY_RUNSHEET_DETAIL] B WITH (NOLOCK)
								WHERE B.[PLAN_RUNSHEET_SN] = A.[PLAN_RUNSHEET_SN]
								AND (ISNULL(B.[ASN_DRAFT_QTY], 0) <> 0
								OR ISNULL(B.[REQUIRED_INHOUSE_PACKAGE], 0) - ISNULL(B.[ASN_DRAFT_QTY], 0) - ISNULL(B.[ASN_CONFIRM_QTY], 0) > 0)
							)
							AND NOT EXISTS
							(
								SELECT
									1
								FROM [LES].[TT_SPM_RUNSHEET_ASN] C WITH (NOLOCK)
								INNER JOIN [LES].[TT_SPM_RUNSHEET_ASN_DETAIL] D WITH (ROWLOCK) ON C.[ID] = D.[ASN_ID]
								WHERE D.[TWD_RUNSHEET_NO] = A.[PLAN_RUNSHEET_NO] AND C.[STATUS] <> 2 AND C.[STATUS] <> 3
							)
						END

					IF EXISTS (SELECT 1 FROM [LES].[TT_TWD_RUNSHEET] A WITH (NOLOCK) WHERE [TWD_RUNSHEET_NO] IN (SELECT [TWD_RUNSHEET_NO] FROM [LES].[TT_WMM_RECEIVE_DETAIL] WITH (NOLOCK) WHERE [RECEIVE_ID] = @RECEIVE_ID))
						BEGIN
							--更新TWD明细实收数量,确认数量
							UPDATE B
							SET B.[ACTUAL_INBOUND_PACKAGE] = ISNULL(B.[ACTUAL_INBOUND_PACKAGE], 0) + C.[ACTUAL_BOX_NUM],
								B.[ACTUAL_INBOUND_PACKAGE_QTY] = ISNULL(B.[ACTUAL_INBOUND_PACKAGE_QTY], 0) + C.[ACTUAL_QTY]
							FROM [LES].[TT_TWD_RUNSHEET] A WITH (NOLOCK)
							INNER JOIN [LES].[TT_TWD_RUNSHEET_DETAIL] B WITH (ROWLOCK) ON A.[TWD_RUNSHEET_SN] = B.[TWD_RUNSHEET_SN]
							INNER JOIN [LES].[TT_WMM_RECEIVE_DETAIL] C WITH (NOLOCK) ON C.[TWD_RUNSHEET_NO] = A.[TWD_RUNSHEET_NO] AND C.[PART_NO] = B.[PART_NO]
							WHERE C.[RECEIVE_ID] = @RECEIVE_ID

							--更新TWD拉动单状态
							--草稿数=0，需求件数 - 草稿数 - 已确认数 - 实收件数 = 0，且拉动单对应的ASN单都已经关闭，则关闭此拉动单
							UPDATE A
							SET [ACTUAL_ARRIVAL_TIME] = GETDATE(),
								[UNLOADING_TIME] = 0,
								[SHEET_STATUS] = 1,
								[UPDATE_DATE] = GETDATE(),
								[UPDATE_USER] = @LOGINAME
							FROM [LES].[TT_TWD_RUNSHEET] A WITH (ROWLOCK)
							WHERE A.[TWD_RUNSHEET_NO] IN
							(
								SELECT DISTINCT [TWD_RUNSHEET_NO] FROM [LES].[TT_WMM_RECEIVE_DETAIL] WITH (NOLOCK) WHERE [RECEIVE_ID] = @RECEIVE_ID
							)
							AND NOT EXISTS
							(
								SELECT
									1
								FROM [LES].[TT_TWD_RUNSHEET_DETAIL] B WITH (NOLOCK)
								WHERE B.[TWD_RUNSHEET_SN] = A.[TWD_RUNSHEET_SN]
								AND (ISNULL(B.[ASN_DRAFT_QTY], 0) <> 0
								OR ISNULL(B.[REQUIRED_INBOUND_PACKAGE_QTY], 0) - ISNULL(B.[ASN_DRAFT_QTY], 0) - ISNULL(B.[ASN_CONFIRM_QTY], 0) > 0)
							)
							AND NOT EXISTS
							(
								SELECT
									1
								FROM [LES].[TT_SPM_RUNSHEET_ASN] C WITH (NOLOCK)
								INNER JOIN [LES].[TT_SPM_RUNSHEET_ASN_DETAIL] D WITH (ROWLOCK) ON C.[ID] = D.[ASN_ID]
								WHERE D.[TWD_RUNSHEET_NO] = A.[TWD_RUNSHEET_NO] AND C.[STATUS] <> 2 AND C.[STATUS] <> 3
							)
						END

					--更新条码状态
					UPDATE [LES].[TT_SPM_DELIVERY_RUNSHEET_BARCODE] WITH (ROWLOCK)
					SET [IS_PRINTED] = 3,
						[UPDATE_USER] = @LOGINAME,
						[UPDATE_DATE] = GETDATE()
					WHERE [TWD_RUNSHEET_NO] IN (SELECT [TWD_RUNSHEET_NO] FROM [LES].[TT_WMM_RECEIVE_DETAIL] WITH (NOLOCK) WHERE [RECEIVE_ID] = @RECEIVE_ID)
					AND [PLAN_ASN_RUNSHEET_NO] IN (SELECT [RECEIVE_NO] FROM [LES].[TT_WMM_RECEIVE] WITH (NOLOCK) WHERE [RECEIVE_ID] = @RECEIVE_ID)
				END
        COMMIT TRANSACTION
    END TRY
    BEGIN CATCH
        --出错，则返回执行不成功，回滚事务
        ROLLBACK TRANSACTION
        --记录错误信息
        INSERT INTO [LES].[TS_SYS_EXCEPTION] ([TIME_STAMP], [APPLICATION], [METHOD], [CLASS], [EXCEPTION_MESSAGE], [ERROR_CODE])
        SELECT GETDATE(), 'INTERFACE', '[LES].[PROC_TWD_UPDATE_RECEIVE_ISCLOSE]', 'Procedure', ERROR_MESSAGE(), ERROR_LINE()
    END CATCH
END