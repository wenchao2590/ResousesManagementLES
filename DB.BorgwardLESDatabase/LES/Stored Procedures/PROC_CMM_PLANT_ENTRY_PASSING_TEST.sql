--EXEC [LES].[PROC_CMM_PLANT_ENTRY_PASSING_TEST] '8400B3D3-8EC8-4496-883D-6AF736C8BEF6','ADMIN'
CREATE PROCEDURE [LES].[PROC_CMM_PLANT_ENTRY_PASSING_TEST]
	@FID UNIQUEIDENTIFIER,
	@LOGIN_USERNAME NVARCHAR(50)
AS
BEGIN
	--任务开始处理
	EXEC [LES].[PROC_SYS_INBOUND_UPDATE] @FID,1,@LOGIN_USERNAME;

	DECLARE @NOW DATETIME=GETDATE();
	DECLARE @ARRIVE_TIME INT,@EXPIRE_TIME INT;
	DECLARE @DOCK NVARCHAR(50);
	DECLARE @VEHICLE_NO NVARCHAR(50);
	DECLARE @SHEET_NO NVARCHAR(50);
	DECLARE @PHONE_NO NVARCHAR(50)=NULL;
	DECLARE @PASSING_FLAG INT;
	DECLARE @IS_URGENT_ORDER INT=0;
	DECLARE @RUNSHEET_CODE NVARCHAR(12);
	DECLARE @PROMISE_ARRIVE_TIME DATETIME=NULL;
	DECLARE @ENTRY_LOG_ID BIGINT=NULL;
	DECLARE @IS_JUMP_QUEUE INT=0;
	DECLARE @DOOR_NO NVARCHAR(16);

	--从接口表获取单号，车牌号，数据标志位
	SELECT TOP 1 @SHEET_NO=[ORDER_NO],@VEHICLE_NO=CARGO_LICENCE,@PASSING_FLAG=PASS_FLAG,@DOOR_NO=DOOR_NO FROM [LES].[TI_MID_RFID_PLANT_ENTRY] WITH(NOLOCK) WHERE FID=@FID
	
	--根据车牌号，单号，获取入厂日志记录表ID
	SELECT TOP 1 @ENTRY_LOG_ID=ID FROM [LES].[TT_CMM_PLANT_ENTRY_LOG] WITH(NOLOCK) WHERE VEHICLE_NO=@VEHICLE_NO AND RUNSHEET_NO=@SHEET_NO
	

	--如果是出厂
	IF @PASSING_FLAG=2 
	BEGIN
		SELECT TOP 1 @ENTRY_LOG_ID=ID FROM [LES].[TT_CMM_PLANT_ENTRY_LOG] WITH(NOLOCK) WHERE VEHICLE_NO=@VEHICLE_NO ORDER BY ID DESC

		--更改入厂协同日志表的出厂时间
		UPDATE [LES].[TT_CMM_PLANT_ENTRY_LOG]
		SET EXIT_TIME=@NOW
		,UPDATE_DATE=GETDATE()
		,UPDATE_USER=@LOGIN_USERNAME
		,STATUS=3--出厂
		WHERE ID=@ENTRY_LOG_ID;

		--存储过程结束
		RETURN;
	END

	/*****************************************
	下面的都是入厂逻辑
	******************************************/
	--先判断单号是否合法,单据状态为已确认的是合法状态
	DECLARE @FID_ASN_NO_MESSAGE UNIQUEIDENTIFIER=NEWID();	
	IF NOT EXISTS(SELECT * FROM [LES].[TT_WMM_RECEIVE] WITH(NOLOCK) WHERE RECEIVE_NO=@SHEET_NO AND CONFIRM_FLAG = 1)
	BEGIN
		INSERT INTO [LES].[TI_MID_RFID_BERTH_INSTRUCT](FID,CARGO_LICENCE,DRIVER_TEL,MSG_CONTEXT,MSG_DATE,IS_ADMIT,DOOR_NO)
			VALUES(@FID_ASN_NO_MESSAGE,@VEHICLE_NO,@PHONE_NO,N'单据号或状态无效，请核对单据状态是否为“已确认”。',GETDATE(),0,@DOOR_NO);

			EXEC [LES].[PROC_SYS_OUTBOUND_INSERT] @FID_ASN_NO_MESSAGE,N'003',N'TI_MID_RFID_BERTH_INSTRUCT',@SHEET_NO,@LOGIN_USERNAME;
		RETURN;
	END
	PRINT 111
	--从RDC收货单获取道口和收货单类型信息
	SELECT TOP 1 @DOCK=DOCK,@RUNSHEET_CODE=RUNSHEET_CODE FROM [LES].[TT_WMM_RECEIVE] WITH(NOLOCK) WHERE [RECEIVE_NO]=@SHEET_NO AND CONFIRM_FLAG = 1

	--从ASN单获取司机电话信息
	SELECT TOP 1 @PHONE_NO=CARRIER_PHONE FROM [LES].[TT_SPM_RUNSHEET_ASN] WITH(NOLOCK) WHERE [ASN_NO]=@SHEET_NO AND STATUS = 1

	--获取系统配置的提前到达时间
	SELECT TOP 1 @ARRIVE_TIME=CAST([PARAMETER_VALUE] AS INT) FROM [LES].[TS_SYS_CONFIG] WITH(NOLOCK)
	WHERE [PARAMETER_NAME]=N'EntryArriveTime'

	--获取系统配置的超时到达时间
	SELECT TOP 1 @EXPIRE_TIME=CAST([PARAMETER_VALUE] AS INT) FROM [LES].[TS_SYS_CONFIG] WITH(NOLOCK)
	WHERE [PARAMETER_NAME]=N'EntryExpireTime'

	--判断是否是紧急单据
	SELECT @IS_URGENT_ORDER=CASE WHEN @RUNSHEET_CODE=N'JIS'
			THEN
			(SELECT CASE WHEN [RUNSHEET_TYPE]=2 THEN 1 ELSE 0 END FROM LES.TT_JIS_RUNSHEET WITH(NOLOCK) WHERE [JIS_RUNSHEET_NO]=@SHEET_NO)
		 WHEN @RUNSHEET_CODE=N'TWD'
			THEN
			(SELECT CASE WHEN [RUNSHEET_TYPE]=2 THEN 1 ELSE 0 END FROM LES.TT_TWD_RUNSHEET WITH(NOLOCK) WHERE [TWD_RUNSHEET_NO]=@SHEET_NO)
		 WHEN @RUNSHEET_CODE=N'CTD'--CTD也是TWD
			THEN
			(SELECT CASE WHEN [RUNSHEET_TYPE]=2 THEN 1 ELSE 0 END FROM LES.TT_TWD_RUNSHEET WITH(NOLOCK) WHERE [TWD_RUNSHEET_NO]=@SHEET_NO)
		 --WHEN @RUNSHEET_CODE=N'PCS'
			--THEN
			--(SELECT CASE WHEN [RUNSHEET_TYPE]=2 THEN 1 ELSE 0 END FROM LES.TT_PCS_RUNSHEET WITH(NOLOCK) WHERE [PCS_RUNSHEET_NO]=@SHEET_NO)
		 --WHEN @RUNSHEET_CODE=N'SPS'
			--THEN
			--(SELECT CASE WHEN [RUNSHEET_TYPE]=2 THEN 1 ELSE 0 END FROM LES.TT_SPS_RUNSHEET WITH(NOLOCK) WHERE [SPS_RUNSHEET_NO]=@SHEET_NO)
		 WHEN @RUNSHEET_CODE=N'ASN'
			THEN
			(SELECT CASE WHEN IS_URGENT=1 THEN 1 ELSE 0 END FROM LES.TT_SPM_RUNSHEET_ASN WITH(NOLOCK) WHERE [ASN_NO]=@SHEET_NO)
		ELSE
			0
	END

	IF @IS_URGENT_ORDER IS NULL
	BEGIN
		SET @IS_URGENT_ORDER=0;
	END
	
	--获取承诺到达时间
	SELECT @PROMISE_ARRIVE_TIME=CASE WHEN @RUNSHEET_CODE=N'JIS'
			THEN
			(SELECT EXPECTED_ARRIVAL_TIME FROM LES.TT_JIS_RUNSHEET WITH(NOLOCK) WHERE [JIS_RUNSHEET_NO]=@SHEET_NO)
		 WHEN @RUNSHEET_CODE=N'TWD'
			THEN
			(SELECT EXPECTED_ARRIVAL_TIME FROM LES.TT_TWD_RUNSHEET WITH(NOLOCK) WHERE [TWD_RUNSHEET_NO]=@SHEET_NO)
		 WHEN @RUNSHEET_CODE=N'CTD'--CTD也是TWD
			THEN
			(SELECT EXPECTED_ARRIVAL_TIME FROM LES.TT_TWD_RUNSHEET WITH(NOLOCK) WHERE [TWD_RUNSHEET_NO]=@SHEET_NO)
		 --WHEN @RUNSHEET_CODE=N'PCS'
			--THEN
			--(SELECT EXPECTED_ARRIVAL_TIME FROM LES.TT_PCS_RUNSHEET WITH(NOLOCK) WHERE [PCS_RUNSHEET_NO]=@SHEET_NO)
		 --WHEN @RUNSHEET_CODE=N'SPS'
			--THEN
			--(SELECT EXPECTED_ARRIVAL_TIME FROM LES.TT_SPS_RUNSHEET WITH(NOLOCK) WHERE [SPS_RUNSHEET_NO]=@SHEET_NO)
		 WHEN @RUNSHEET_CODE=N'ASN'
			THEN
			(SELECT PROMISE_DELIVERY_TIME FROM LES.TT_SPM_RUNSHEET_ASN WITH(NOLOCK) WHERE [ASN_NO]=@SHEET_NO)
		 ELSE--最后一种情况就是交货单
			(SELECT EXPECTED_ARRIVAL_TIME FROM LES.TT_SPM_DELIVERY_RUNSHEET WITH(NOLOCK) WHERE PLAN_RUNSHEET_NO=@SHEET_NO)
	END

	PRINT 222
	--开始事务处理
	BEGIN TRAN

		--未到达期望入厂时间，不允许入厂，告诉司机去厂外等待区等待
		IF @NOW<DATEADD(MINUTE,-@ARRIVE_TIME,@PROMISE_ARRIVE_TIME)
		BEGIN
			--插入数据到短信接口表，发送短信给司机去厂外等待区等待
			DECLARE @FID_WAITING_OUTDOOR_MESSAGE UNIQUEIDENTIFIER=NEWID();

			DECLARE @EXPECT_TIME DATETIME=DATEADD(MINUTE,-@ARRIVE_TIME,@PROMISE_ARRIVE_TIME);
			INSERT INTO [LES].[TI_MID_RFID_BERTH_INSTRUCT](FID,CARGO_LICENCE,DRIVER_TEL,MSG_CONTEXT,MSG_DATE,IS_ADMIT,DOOR_NO)
			VALUES(@FID_WAITING_OUTDOOR_MESSAGE,@VEHICLE_NO,@PHONE_NO,N'未到达期望入厂时间请司机前往厂外等待区等待，期望入厂时间为【'+
			CAST(YEAR(@EXPECT_TIME) AS NVARCHAR(10))+N'年'+CAST(MONTH(@EXPECT_TIME) AS NVARCHAR(10))+N'月'+CAST(DAY(@EXPECT_TIME) AS NVARCHAR(10))
			+N'日 '+CAST(DATENAME(HOUR,@EXPECT_TIME) AS NVARCHAR(10))+N'点'+CAST(DATENAME(MINUTE,@EXPECT_TIME) AS NVARCHAR(10))+N'分'+N'】',GETDATE(),0,@DOOR_NO);

			EXEC [LES].[PROC_SYS_OUTBOUND_INSERT] @FID_WAITING_OUTDOOR_MESSAGE,N'003',N'TI_MID_RFID_BERTH_INSTRUCT',@DOCK,@LOGIN_USERNAME;

			--存储过程结束
			COMMIT
			RETURN
		END

		DECLARE @WAITING_AREA_ID INT;
		DECLARE @WAITING_AREA_NAME NVARCHAR(100);
		PRINT 333
		--获取等待区信息
		SELECT TOP 1 @WAITING_AREA_ID=AREA_ID FROM [LES].[TM_BAS_DOCK] WITH(NOLOCK) WHERE DOCK=@DOCK;
		SELECT TOP 1 @WAITING_AREA_NAME=AREA_NAME FROM [LES].[TM_BAS_WAITING_AREA] WITH(NOLOCK) WHERE AREA_ID=@WAITING_AREA_ID

		DECLARE @DOCK_STATUS INT;

		SELECT TOP 1 @DOCK_STATUS=[STATUS] FROM [LES].[TT_CMM_DOCK_STATUS] WHERE DOCK=@DOCK

		--如果当前订单的道口被占用了，查看该等待区的其它道口是否可以卸货
		IF @DOCK_STATUS=1
		BEGIN
			SELECT TOP 1 @DOCK=DS.DOCK, @DOCK_STATUS=0
			FROM [LES].[TT_CMM_DOCK_STATUS] AS DS WITH(NOLOCK)
			INNER JOIN
			LES.TM_BAS_DOCK AS DOCK WITH(NOLOCK)
			ON DS.DOCK=DOCK.DOCK
			INNER JOIN 
			[LES].[TT_CMM_WAITING_QUEUE] AS QUE WITH(NOLOCK)
			ON DOCK.AREA_ID=QUE.AREA_ID
			WHERE DS.[STATUS]=0
		END

		--如果该车牌号的订单号在入厂协同日志表中不存在，那么插入该条记录到入厂协同日志表
		IF @ENTRY_LOG_ID IS NULL
		BEGIN
			INSERT INTO [LES].[TT_CMM_PLANT_ENTRY_LOG](VEHICLE_NO,RUNSHEET_NO,IS_URGENT_ORDER,REQUIRE_TIME,DOCK,[STATUS],CREATE_USER,CREATE_DATE)
			VALUES(@VEHICLE_NO,@SHEET_NO,@IS_URGENT_ORDER,@PROMISE_ARRIVE_TIME,@DOCK,0,@LOGIN_USERNAME,GETDATE());

			SELECT @ENTRY_LOG_ID=SCOPE_IDENTITY();
		END
		PRINT 444
		--判断及更新该车是否已经超时
		SELECT @IS_JUMP_QUEUE=CASE WHEN @NOW>DATEADD(MINUTE,@EXPIRE_TIME,@PROMISE_ARRIVE_TIME) THEN 1 ELSE 0 END;
		UPDATE [LES].[TT_CMM_PLANT_ENTRY_LOG] SET IS_JUMP_QUEUE=@IS_JUMP_QUEUE WHERE ID=@ENTRY_LOG_ID;

		/*****************************************
		正常队列排队，正常道口卸货
		******************************************/
		PRINT 555
		PRINT @IS_JUMP_QUEUE
		PRINT @DOCK_STATUS
		PRINT @IS_URGENT_ORDER
		--该车没有超时，道口未被占用且等待区无其它车正在排队，并且单据不是紧急单据，那么直接安排该车去相应道口卸货
		IF @IS_JUMP_QUEUE=0 AND @DOCK_STATUS=0 AND NOT EXISTS (SELECT 1 FROM [LES].[TT_CMM_WAITING_QUEUE] WHERE AREA_ID=@WAITING_AREA_ID) AND @IS_URGENT_ORDER=0
		BEGIN
			--更新道口状态为占用,并加入车牌号和订单号信息
			UPDATE [LES].[TT_CMM_DOCK_STATUS] SET [STATUS]=1,[VEHICLE_NO]=@VEHICLE_NO,[SHEET_NO]=@SHEET_NO,UPDATE_DATE=GETDATE(),UPDATE_USER=@LOGIN_USERNAME WHERE DOCK=@DOCK;

			--插入数据到短信接口表，发送短信给司机前往相应道口卸货
			DECLARE @FID_DOCK_HOLD_MESSAGE UNIQUEIDENTIFIER=NEWID();

			INSERT INTO [LES].[TI_MID_RFID_BERTH_INSTRUCT](FID,CARGO_LICENCE,DRIVER_TEL,MSG_CONTEXT,MSG_DATE,IS_ADMIT,DOOR_NO)
			VALUES(@FID_DOCK_HOLD_MESSAGE,@VEHICLE_NO,@PHONE_NO,N'请司机前往道口【'+ISNULL(@DOCK,N'')+N'】开始卸货',GETDATE(),1,@DOOR_NO);

			EXEC [LES].[PROC_SYS_OUTBOUND_INSERT] @FID_DOCK_HOLD_MESSAGE,N'003',N'TI_MID_RFID_BERTH_INSTRUCT',@DOCK,@LOGIN_USERNAME;
			
			--更改入厂协同日志表的入厂时间
			UPDATE [LES].[TT_CMM_PLANT_ENTRY_LOG]
			SET ENTRY_TIME=@NOW
			,UPDATE_DATE=GETDATE()
			,UPDATE_USER=@LOGIN_USERNAME
			WHERE ID=@ENTRY_LOG_ID;
		END

		--该车没有超时，但道口被占用且等待区有位置可以排队，该单据不是紧急单据，那么安排该车去相应等待区排队
		IF @IS_JUMP_QUEUE=0 AND @DOCK_STATUS=1 AND @IS_URGENT_ORDER=0 AND (SELECT COUNT(1) FROM [LES].[TT_CMM_WAITING_QUEUE] WHERE AREA_ID=@WAITING_AREA_ID)<(SELECT TOP 1 AREA_CAPACITY FROM [LES].[TM_BAS_WAITING_AREA] WHERE AREA_ID = @WAITING_AREA_ID)
		BEGIN
			--插入该车到相应等待区队列
			INSERT INTO [LES].[TT_CMM_WAITING_QUEUE]
			(VEHICLE_NO,RUNSHEET_NO,AREA_ID,DOCK,IS_URGENT_ORDER,SEQUENCE,CREATE_USER,CREATE_DATE,IS_JUMP_QUEUE)
			VALUES
			(
				@VEHICLE_NO,
				@SHEET_NO,
				@WAITING_AREA_ID,
				@DOCK,
				@IS_URGENT_ORDER,
				ISNULL((SELECT MAX(SEQUENCE) FROM [LES].[TT_CMM_WAITING_QUEUE] WHERE AREA_ID=@WAITING_AREA_ID),0)+1,--队列中的最大排队号加一
				@LOGIN_USERNAME,
				GETDATE(),
				0
			);

			--插入数据到短信接口表，发送短信给司机前往相应等待区排队
			DECLARE @FID_QUEUE_MESSAGE UNIQUEIDENTIFIER=NEWID();

			INSERT INTO [LES].[TI_MID_RFID_BERTH_INSTRUCT](FID,CARGO_LICENCE,DRIVER_TEL,MSG_CONTEXT,MSG_DATE,IS_ADMIT,DOOR_NO)
			VALUES(@FID_QUEUE_MESSAGE,@VEHICLE_NO,@PHONE_NO,N'请司机前往等待区【'+ISNULL(@WAITING_AREA_NAME,N'')+N'】排队',GETDATE(),1,@DOOR_NO);

			EXEC [LES].[PROC_SYS_OUTBOUND_INSERT] @FID_QUEUE_MESSAGE,N'003',N'TI_MID_RFID_BERTH_INSTRUCT',@WAITING_AREA_NAME,@LOGIN_USERNAME;

			--更改入厂协同日志表的入厂时间
			UPDATE [LES].[TT_CMM_PLANT_ENTRY_LOG]
			SET ENTRY_TIME=@NOW
			,UPDATE_DATE=GETDATE()
			,UPDATE_USER=@LOGIN_USERNAME
			,STATUS=1--状态为在厂内等待区等待
			WHERE ID=@ENTRY_LOG_ID;
		END

		--该车没有超时，不是紧急单据，道口被占用且等待区没有位置可以排队，那么安排该车去厂外等待区继续等待
		IF @IS_JUMP_QUEUE=0 AND @IS_URGENT_ORDER=0 AND @DOCK_STATUS=1 AND (SELECT COUNT(1) FROM [LES].[TT_CMM_WAITING_QUEUE] WHERE AREA_ID=@WAITING_AREA_ID)>=(SELECT TOP 1 AREA_CAPACITY FROM [LES].[TM_BAS_WAITING_AREA] WHERE AREA_ID = @WAITING_AREA_ID)
		BEGIN
			--插入数据到短信接口表，发送短信给司机去厂外等待区等待
			DECLARE @FID_FULL_QUEUE_WAITING_OUTDOOR_MESSAGE UNIQUEIDENTIFIER=NEWID();

			DECLARE @DEADLINE_TIME DATETIME=DATEADD(MINUTE,@EXPIRE_TIME,@PROMISE_ARRIVE_TIME);
			INSERT INTO [LES].[TI_MID_RFID_BERTH_INSTRUCT](FID,CARGO_LICENCE,DRIVER_TEL,MSG_CONTEXT,MSG_DATE,IS_ADMIT,DOOR_NO)
			VALUES(@FID_FULL_QUEUE_WAITING_OUTDOOR_MESSAGE,@VEHICLE_NO,@PHONE_NO,N'等待区【'+@WAITING_AREA_NAME+N'】车辆排队已满，请司机继续在厂外等待区等候，请在【'
			+CAST(YEAR(@DEADLINE_TIME) AS NVARCHAR(10))+N'年'+CAST(MONTH(@DEADLINE_TIME) AS NVARCHAR(10))+N'月'+CAST(DAY(@DEADLINE_TIME) AS NVARCHAR(10))+N'日 '+
			CAST(DATENAME(HOUR,@DEADLINE_TIME) AS NVARCHAR(10))+N'点'+CAST(DATENAME(MINUTE,@DEADLINE_TIME) AS NVARCHAR(10))+N'分'+N'】前尝试再次入厂，否则将超时',
			GETDATE(),0,@DOOR_NO);

			EXEC [LES].[PROC_SYS_OUTBOUND_INSERT] @FID_FULL_QUEUE_WAITING_OUTDOOR_MESSAGE,N'003',N'TI_MID_RFID_BERTH_INSTRUCT',@WAITING_AREA_NAME,@LOGIN_USERNAME;
		END

		/*****************************************
		异常队列排队，紧急道口卸货
		******************************************/

		--如果该车已经超时或者是紧急单据，那么就在异常队列排队，或去紧急卸货道口
		IF @IS_JUMP_QUEUE=1 OR @IS_URGENT_ORDER=1
		BEGIN
			--获取紧急等待区异常队列信息
			SELECT TOP 1 @WAITING_AREA_ID=AREA_ID,@WAITING_AREA_NAME=AREA_NAME FROM [LES].[TM_BAS_WAITING_AREA] WHERE IS_URGENT=1
			
			DECLARE @URGENT_DOCK NVARCHAR(10)=NULL;

			--获取是否有空闲的紧急道口
			SELECT TOP 1 @URGENT_DOCK=DOCK.DOCK 
			FROM [LES].[TM_BAS_WAITING_AREA] AS AREA WITH(NOLOCK) 
			INNER JOIN [LES].[TM_BAS_DOCK] AS DOCK WITH(NOLOCK) 
			ON AREA.AREA_ID=DOCK.AREA_ID
			INNER JOIN [LES].[TT_CMM_DOCK_STATUS] AS DOCK_STATUS WITH(NOLOCK) 
			ON DOCK.DOCK=DOCK_STATUS.DOCK
			WHERE DOCK_STATUS.STATUS=0 AND DOCK.AREA_ID=@WAITING_AREA_ID

			--异常队列没有排队的车，且有空闲的紧急道口，让该车直接去紧急道口卸货
			IF (SELECT COUNT(1) FROM [LES].[TM_BAS_WAITING_AREA] WHERE AREA_ID=@WAITING_AREA_ID)=0 AND @URGENT_DOCK IS NOT NULL
			BEGIN
				--更新紧急道口状态为占用,并加入车牌号和订单号信息
				UPDATE [LES].[TT_CMM_DOCK_STATUS] SET [STATUS]=1,[VEHICLE_NO]=@VEHICLE_NO,[SHEET_NO]=@SHEET_NO,UPDATE_DATE=GETDATE(),UPDATE_USER=@LOGIN_USERNAME WHERE DOCK=@URGENT_DOCK;

				--插入数据到短信接口表，发送短信给司机前往相应道口卸货
				DECLARE @FID_EXCEPTION_DOCK_HOLD_MESSAGE UNIQUEIDENTIFIER=NEWID();

				INSERT INTO [LES].[TI_MID_RFID_BERTH_INSTRUCT](FID,CARGO_LICENCE,DRIVER_TEL,MSG_CONTEXT,MSG_DATE,IS_ADMIT,DOOR_NO)
				VALUES(@FID_EXCEPTION_DOCK_HOLD_MESSAGE,@VEHICLE_NO,@PHONE_NO,N'请司机前往道口【'+ISNULL(@DOCK,N'')+N'】开始卸货',GETDATE(),1,@DOOR_NO);

				EXEC [LES].[PROC_SYS_OUTBOUND_INSERT] @FID_EXCEPTION_DOCK_HOLD_MESSAGE,N'003',N'TI_MID_RFID_BERTH_INSTRUCT',@DOCK,@LOGIN_USERNAME;

			END
			--异常队列有排队的车，或者没有空闲的紧急道口，那么该车要去异常队列排队
			ELSE
			BEGIN
				--如果是紧急订单并且已经超时，那么插队到异常队列
				IF @IS_URGENT_ORDER=1 AND @IS_JUMP_QUEUE=1
				BEGIN
					--插队到异常队列
					INSERT INTO [LES].[TT_CMM_WAITING_QUEUE]
					(VEHICLE_NO,RUNSHEET_NO,AREA_ID,DOCK,IS_URGENT_ORDER,SEQUENCE,CREATE_USER,CREATE_DATE,IS_JUMP_QUEUE)
					VALUES
					(
						@VEHICLE_NO,
						@SHEET_NO,
						@WAITING_AREA_ID,
						@DOCK,
						@IS_URGENT_ORDER,
						ISNULL((SELECT MAX(SEQUENCE) FROM [LES].[TT_CMM_WAITING_QUEUE] WHERE AREA_ID=@WAITING_AREA_ID AND IS_JUMP_QUEUE=1),0)+1,--队列中的最大插队排队号加一
						@LOGIN_USERNAME,
						GETDATE(),
						1--插队
					);

					--插队后队列后面的排队号全部加一
					UPDATE [LES].[TT_CMM_WAITING_QUEUE] 
					SET SEQUENCE=SEQUENCE+1
					WHERE
					AREA_ID=@WAITING_AREA_ID
					AND
					IS_JUMP_QUEUE=0
				END
				--紧急订单没有超时，或者普通订单超时，那么排队到异常队列
				ELSE
				BEGIN
					INSERT INTO [LES].[TT_CMM_WAITING_QUEUE]
					(VEHICLE_NO,RUNSHEET_NO,AREA_ID,DOCK,IS_URGENT_ORDER,SEQUENCE,CREATE_USER,CREATE_DATE,IS_JUMP_QUEUE)
					VALUES
					(
						@VEHICLE_NO,
						@SHEET_NO,
						@WAITING_AREA_ID,
						@DOCK,
						@IS_URGENT_ORDER,
						ISNULL((SELECT MAX(SEQUENCE) FROM [LES].[TT_CMM_WAITING_QUEUE] WHERE AREA_ID=@WAITING_AREA_ID),0)+1,--排队号为队列中的最大排队号加1
						@LOGIN_USERNAME,
						GETDATE(),
						0--排队
					);
				END

				--插入数据到短信接口表，发送短信给司机前往相应等待区排队
				DECLARE @FID_EXCEPTION_QUEUE_MESSAGE UNIQUEIDENTIFIER=NEWID();

				INSERT INTO [LES].[TI_MID_RFID_BERTH_INSTRUCT](FID,CARGO_LICENCE,DRIVER_TEL,MSG_CONTEXT,MSG_DATE,IS_ADMIT,DOOR_NO)
				VALUES(@FID_EXCEPTION_QUEUE_MESSAGE,@VEHICLE_NO,@PHONE_NO,N'请司机前往等待区【'+ISNULL(@WAITING_AREA_NAME,N'')+N'】排队',GETDATE(),1,@DOOR_NO);

				EXEC [LES].[PROC_SYS_OUTBOUND_INSERT] @FID_EXCEPTION_QUEUE_MESSAGE,N'003',N'TI_MID_RFID_BERTH_INSTRUCT',@WAITING_AREA_NAME,@LOGIN_USERNAME;

				--更改入厂协同日志表的状态
				UPDATE [LES].[TT_CMM_PLANT_ENTRY_LOG]
				SET 
				UPDATE_DATE=GETDATE()
				,UPDATE_USER=@LOGIN_USERNAME
				,STATUS=1--状态为在厂内等待区等待
				WHERE ID=@ENTRY_LOG_ID;
			END

			--更改入厂协同日志表的入厂时间
			UPDATE [LES].[TT_CMM_PLANT_ENTRY_LOG]
			SET ENTRY_TIME=@NOW
			,UPDATE_DATE=GETDATE()
			,UPDATE_USER=@LOGIN_USERNAME
			WHERE ID=@ENTRY_LOG_ID;
		END
	COMMIT

END