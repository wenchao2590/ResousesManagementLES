CREATE PROCEDURE [LES].[PROC_WMM_RECEIVE_SUBMITBOX_New]
(
	@RECEIVE_ID int,
	@PART_NO nvarchar(50),
	@BARCODE_DATA NVARCHAR(50),
	@ActualQty int,
	@Create_User NVARCHAR(50)
)
AS
BEGIN
	BEGIN TRY
		BEGIN TRANSACTION

		DECLARE @LASTNUM DECIMAL(18,2)
		DECLARE @RECEIVE_NO NVARCHAR(50)
		DECLARE @RUNSHEET_NO NVARCHAR(50)

		SELECT @RECEIVE_NO = RECEIVE_NO FROM [LES].[TT_WMM_RECEIVE] WITH (NOLOCK) WHERE [RECEIVE_ID] = @RECEIVE_ID
		SELECT @RUNSHEET_NO = [TWD_RUNSHEET_NO] FROM [LES].[TT_SPM_DELIVERY_RUNSHEET_BARCODE] WITH (NOLOCK) WHERE [BARCODE_DATA] = @BARCODE_DATA
		SET @ActualQty = ISNULL(@ActualQty, 0)

		IF EXISTS(SELECT 1 FROM [LES].[TM_WMM_TRAN_DETAILS] WITH (NOLOCK) WHERE [BARCODE_DATA] = @BARCODE_DATA AND [TRAN_TYPE] = 1 AND [TRAN_STATE] = 0)
			BEGIN
				SELECT @LASTNUM = ISNULL([NUM], 0) FROM [LES].[TM_WMM_TRAN_DETAILS] WITH (NOLOCK) WHERE [BARCODE_DATA] = @BARCODE_DATA  AND [TRAN_TYPE] = 1 AND [TRAN_STATE] = 0
			
				UPDATE [LES].[TT_WMM_RECEIVE_DETAIL] WITH (ROWLOCK)
				SET [Current_QTY] = ISNULL([Current_QTY], 0) - @LASTNUM + @ActualQty
				WHERE [RECEIVE_ID] = @RECEIVE_ID AND [PART_NO] = @PART_NO AND (ISNULL([TWD_RUNSHEET_NO], '') = '' OR [TWD_RUNSHEET_NO] = @RUNSHEET_NO)

				UPDATE [LES].[TM_WMM_TRAN_DETAILS] WITH (ROWLOCK) SET [NUM] = @ActualQty WHERE [TRAN_NO] = @RECEIVE_NO AND [BARCODE_DATA] = @BARCODE_DATA
			END
		ELSE
			BEGIN
				UPDATE [LES].[TT_WMM_RECEIVE_DETAIL] WITH (ROWLOCK)
				SET [Current_QTY] = ISNULL([Current_QTY], 0) + @ActualQty,
					[Current_BOX_NUM] = ISNULL([Current_BOX_NUM], 0) + 1 
				WHERE [RECEIVE_ID] = @RECEIVE_ID AND [PART_NO] = @PART_NO AND (ISNULL([TWD_RUNSHEET_NO], '') = '' OR [TWD_RUNSHEET_NO] = @RUNSHEET_NO)

				INSERT INTO [LES].[TM_WMM_TRAN_DETAILS]
				(
					[BARCODE_DATA],
					[PART_NO],
					[BOX_NUM],
					[NUM],
					[PLANT],
					[WM_NO],
					[ZONE_NO],
					[DLOC],
					[PACKAGE_MODEL],
					[PACKAGE],
					[TRAN_STATE],
					[SUPPLIER_NUM],
					[BOX_PARTS],
					[PART_CNAME],
					[MEASURING_UNIT_NO],
					[TRAN_TYPE],
					[TRAN_NO],
					[CREATE_DATE],
					[CREATE_USER],
					[TRAN_DATE]
				)
				SELECT
					@BARCODE_DATA,			--BARCODE_DATA
					@PART_NO,				--PART_NO
					1,						--BOX_NUM
					@ActualQty,				--NUM
					A.[PLANT],				--PLANT
					A.[WM_NO],				--WM_NO
					A.[ZONE_NO],			--ZONE_NO
					B.[DLOC],				--DLOC
					B.[PACKAGE_MODEL],		--PACKAGE_MODEL
					B.[PACKAGE],			--PACKAGE
					0,						--TRAN_STATE
					A.[SUPPLIER_NUM],		--SUPPLIER_NUM
					B.[BOX_PARTS],			--BOX_PARTS
					B.[PART_CNAME],			--PART_CNAME
					B.[MEASURING_UNIT_NO],	--MEASURING_UNIT_NO
					1,						--TRAN_TYPE
					A.[RECEIVE_NO],			--TRAN_NO
					GETDATE(),				--CREATE_DATE
					@Create_User,			--CREATE_USER
					GETDATE()				--TRAN_DATE
				FROM [LES].[TT_WMM_RECEIVE] A WITH (NOLOCK)
				INNER JOIN [LES].[TT_WMM_RECEIVE_DETAIL] B WITH (NOLOCK) ON A.[RECEIVE_ID] = B.[RECEIVE_ID]
				WHERE A.[RECEIVE_ID] = @RECEIVE_ID AND B.[PART_NO] = @PART_NO AND (ISNULL([TWD_RUNSHEET_NO], '') = '' OR [TWD_RUNSHEET_NO] = @RUNSHEET_NO)
			END

		COMMIT TRANSACTION
	END TRY   
	BEGIN CATCH
		ROLLBACK TRANSACTION
	END CATCH
END