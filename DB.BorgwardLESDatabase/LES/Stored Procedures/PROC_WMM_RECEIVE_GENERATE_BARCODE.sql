CREATE PROCEDURE [LES].[PROC_WMM_RECEIVE_GENERATE_BARCODE]
(
	@ReceiveId INT
)
AS
BEGIN
	SET NOCOUNT ON
	DECLARE @RECEIVE_NO NVARCHAR(20)
	DECLARE @WM_NO NVARCHAR(10)
	DECLARE @ZONE_NO NVARCHAR(20)
	DECLARE @DLOC NVARCHAR(20)
	DECLARE @PART_NO NVARCHAR(30)
	DECLARE @PART_CNAME NVARCHAR(100)
	DECLARE @PACKAGE_MODEL NVARCHAR(30)
	DECLARE @PACKAGE INT
	DECLARE @NUM INT
	DECLARE @MEASURING_UNIT_NO NVARCHAR(10)
	DECLARE @SUPPLIER_NUM NVARCHAR(24)
	DECLARE @SUPPLIER_NAME NVARCHAR(100)
	DECLARE @BOX_PARTS NVARCHAR(10)
	DECLARE @PLANT NVARCHAR(5)
	DECLARE @PART_NICKNAME NVARCHAR(50)
	DECLARE @DOCK NVARCHAR(10)
	DECLARE @TWD_RUNSHEET_NO NVARCHAR(36)
	DECLARE @LINE_POSITION NVARCHAR(100)
	DECLARE @INNER_LOCATION NVARCHAR(50)
	DECLARE @TOTAL INT
	DECLARE @RECEIVE_DETAIL_ID INT

	SELECT
		@PLANT = A.[PLANT],
		@RECEIVE_NO = A.[RECEIVE_NO],
		@DOCK = A.[DOCK],
		@SUPPLIER_NUM = A.[SUPPLIER_NUM],
		@SUPPLIER_NAME = B.[SUPPLIER_NAME]
	FROM [LES].[TT_WMM_RECEIVE] A WITH (NOLOCK)
	LEFT JOIN [LES].[TM_BAS_SUPPLIER] B WITH (NOLOCK) ON A.[SUPPLIER_NUM] = B.[SUPPLIER_NUM]
	WHERE [RECEIVE_ID] = @ReceiveId

	SELECT @RECEIVE_DETAIL_ID = MIN([RECEIVE_DETAIL_ID]) FROM [LES].[TT_WMM_RECEIVE_DETAIL] WITH (NOLOCK) WHERE [RECEIVE_ID] = @ReceiveId
	WHILE @RECEIVE_DETAIL_ID IS NOT NULL
		BEGIN
			SELECT
				@WM_NO = [WM_NO],
				@ZONE_NO = [ZONE_NO],
				@DLOC = [DLOC], 
				@PART_NO = [PART_NO],
				@PART_CNAME = [PART_CNAME],
				@PACKAGE_MODEL = [PACKAGE_MODEL],
				@PACKAGE = [PACKAGE],
				@NUM = [REQUIRED_QTY],
				@BOX_PARTS = [BOX_PARTS],
				@TWD_RUNSHEET_NO = [TWD_RUNSHEET_NO]
			FROM [LES].[TT_WMM_RECEIVE_DETAIL] WITH (NOLOCK)
			WHERE [RECEIVE_DETAIL_ID] = @RECEIVE_DETAIL_ID

			IF @ZONE_NO = '2016'
				BEGIN
					--获取库位地址
					SELECT TOP 1 @DLOC = [DLOC] FROM [LES].[TM_BAS_PARTS_STOCK] WITH (NOLOCK) WHERE  [PLANT] = @PLANT AND [ZONE_NO] = '2015' AND [PART_NO] = @PART_NO
					--获取超市地址
					SELECT TOP 1 @INNER_LOCATION = [DLOC] FROM [LES].[TM_BAS_PARTS_STOCK] WITH (NOLOCK) WHERE  [PLANT] = @PLANT AND [ZONE_NO] = '2016' AND [PART_NO] = @PART_NO
				END
			ELSE
				BEGIN
					--获取库位地址
					SELECT TOP 1 @DLOC = [DLOC] FROM [LES].[TM_BAS_PARTS_STOCK] WITH (NOLOCK) WHERE  [PLANT] = @PLANT AND [WM_NO] = @WM_NO AND [ZONE_NO] = @ZONE_NO AND [PART_NO] = @PART_NO
					--获取超市地址
					SELECT TOP 1 @INNER_LOCATION = [DLOC] FROM [LES].[TM_BAS_PARTS_STOCK] WITH (NOLOCK) WHERE  [PLANT] = @PLANT AND [PART_NO] = @PART_NO AND [WM_NO] = @WM_NO AND [ZONE_NO] IN
					(
						SELECT [REPACKAGE_ZONE] FROM [LES].[TM_WMM_ZONES] WITH (NOLOCK) WHERE [PLANT] = @PLANT AND [WM_NO] = @WM_NO AND [ZONE_NO] = @ZONE_NO
					)
				END

			--获取单位、昵称
			SELECT
				@MEASURING_UNIT_NO = [PART_UNITS]
			FROM [LES].[TM_BAS_MAINTAIN_PARTS] WITH (NOLOCK)
			WHERE [PART_NO] = @PART_NO
			--获取昵称
			SELECT
				@PART_NICKNAME = [PART_NICKNAME]
			FROM [LES].[TM_BAS_PARTS_STOCK] WITH (NOLOCK)
			WHERE [PLANT] = @PLANT AND [WM_NO] = @WM_NO AND [ZONE_NO] = @ZONE_NO AND [PART_NO] = @PART_NO

			--从拉动关系中获取零件昵称及线边地址
			SELECT TOP 1
				@LINE_POSITION = L.[STORAGE_LOCATION] 
			FROM [LES].[TM_BAS_PARTS_STOCK] S WITH (NOLOCK) 
			INNER JOIN [LES].[TM_BAS_MAINTAIN_INHOUSE_LOGISTIC_STANDARD] L WITH (NOLOCK)
			ON L.[PLANT] = S.[PLANT] AND L.[PART_NO] = S.[PART_NO] and L.[ASSEMBLY_LINE] = S.[ASSEMBLY_LINE]
			WHERE S.[PLANT] = @PLANT AND S.[PART_NO] = @PART_NO AND S.[ZONE_NO] = ISNULL(@ZONE_NO, '') AND ISNULL(L.[STORAGE_LOCATION], '') <> ''
			IF	(SELECT COUNT(1) FROM 
				[LES].[TM_BAS_PARTS_STOCK] S WITH (NOLOCK) 
				INNER JOIN [LES].[TM_BAS_MAINTAIN_INHOUSE_LOGISTIC_STANDARD] L WITH (NOLOCK)
				ON L.[PLANT] = S.[PLANT] AND L.[PART_NO] = S.[PART_NO] AND L.[ASSEMBLY_LINE] = S.[ASSEMBLY_LINE]
				WHERE S.[PLANT] = @PLANT AND S.[PART_NO] = @PART_NO AND S.[ZONE_NO] = ISNULL(@ZONE_NO, '') AND ISNULL(L.[STORAGE_LOCATION], '') <> '')>1
			BEGIN
				SET @LINE_POSITION = @LINE_POSITION + '...'
			END

			SET @TOTAL = @NUM
			WHILE (@TOTAL > 0)
				BEGIN
					DECLARE @barCodeNo NVARCHAR(32) 
					EXEC LES.[PROC_SYS_GET_NEXT_BARCODE_SEQUENCE] 'BARCODE', @barCodeNo OUTPUT

					IF(@TOTAL < @PACKAGE)
						BEGIN
							SET @NUM = @TOTAL
						END
					ELSE
						BEGIN
							SET @NUM = @PACKAGE
						END
				  
					INSERT INTO [LES].[TT_SPM_DELIVERY_RUNSHEET_BARCODE]
					(
						[PLAN_ASN_RUNSHEET_NO],
						[PART_NO],
						[PART_CNAME],
						[BARCODE_DATA],
						[INHOUSE_PACKAGE_MODEL],
						[REQUIRED_INBOUND_PACKAGE_QTY],
						[CREATE_DATE],
						[SUPPLIER_NUM],
						[SUPPLIER_NAME],
						[BOX_PARTS],
						[MEASURING_UNIT_NO],
						[TWD_RUNSHEET_NO],
						[PLANT],
						[STORAGE_LOCATION],
						[INNER_LOCATION],
						[PART_NICKNAME],
						[LINE_POSITION],
						[REQUIRED_DATE],
						[DOCK]
					)
					VALUES
					(
						@RECEIVE_NO,
						@PART_NO,
						@PART_CNAME,
						@barCodeNo,
						@PACKAGE_MODEL,
						@NUM,
						GETDATE(),
						ISNULL(@SUPPLIER_NUM, ''),
						ISNULL(@SUPPLIER_NAME, ''),
						ISNULL(@BOX_PARTS, ''),
						ISNULL(@MEASURING_UNIT_NO, ''),
						@TWD_RUNSHEET_NO,
						@PLANT,
						@DLOC,
						@INNER_LOCATION,
						@PART_NICKNAME,
						@LINE_POSITION,
						GETDATE(),
						@DOCK
					)

					SET @TOTAL = @TOTAL - @NUM
				END

			SET @TOTAL = 0
			SET @NUM = 0
			SELECT @RECEIVE_DETAIL_ID = MIN([RECEIVE_DETAIL_ID]) FROM [LES].[TT_WMM_RECEIVE_DETAIL] WITH (NOLOCK) WHERE [RECEIVE_ID] = @ReceiveId AND [RECEIVE_DETAIL_ID] > @RECEIVE_DETAIL_ID
		END
END