/********************************************************************/
/*   Project Name:  Foton LES System								*/
/*   Program Name:  [LES].[PROC_TWD_UPDATE_OUTPUT_ISCLOSE]			*/
/*   Called By:     by the Page										*/
/*   Purpose:       TWD拉动出库完成时更改相应拉动单数据			    */
/*   author:        孙述霄    2017-09-28							*/
/********************************************************************/
CREATE PROCEDURE [LES].[PROC_TWD_UPDATE_OUTPUT_ISCLOSE]
(
	@OUTPUT_ID INT,					--出库单ID号
	@RUNSHEET_CODE NVARCHAR(12),	--交易类别
	@LOGINAME NVARCHAR(50)			--处理人
)
AS
BEGIN
    BEGIN TRY    
        BEGIN TRANSACTION
			IF @RUNSHEET_CODE = 'TWD'
				BEGIN
					--更新TWD明细实收数量
					UPDATE B
					SET B.[ACTUAL_INBOUND_PACKAGE] = D.[ACTUAL_BOX_NUM],
						B.[ACTUAL_INBOUND_PACKAGE_QTY] = D.[ACTUAL_QTY]
					FROM [LES].[TT_TWD_RUNSHEET] A WITH (NOLOCK)
					INNER JOIN [LES].[TT_TWD_RUNSHEET_DETAIL] B WITH (ROWLOCK) ON A.[TWD_RUNSHEET_SN] = B.[TWD_RUNSHEET_SN]
					INNER JOIN [LES].[TT_WMM_OUTPUT] C WITH (NOLOCK) ON C.[OUTPUT_NO] = A.[TWD_RUNSHEET_NO]
					INNER JOIN [LES].[TT_WMM_OUTPUT_DETAIL] D WITH (NOLOCK) ON D.[OUTPUT_ID] = C.[OUTPUT_ID] AND D.[PART_NO] = B.[PART_NO]
					WHERE C.[OUTPUT_ID] = @OUTPUT_ID

					--更新TWD拉动单状态
					UPDATE [LES].[TT_TWD_RUNSHEET] WITH (ROWLOCK)
					SET [ACTUAL_ARRIVAL_TIME] = GETDATE(),
						[UNLOADING_TIME] = 0,
						[SHEET_STATUS] = 1,
						[UPDATE_DATE] = GETDATE(),
						[UPDATE_USER] = @LOGINAME
					WHERE [TWD_RUNSHEET_NO] IN (SELECT [OUTPUT_NO] FROM [LES].[TT_WMM_OUTPUT] WITH (NOLOCK) WHERE [OUTPUT_ID] = @OUTPUT_ID)
				END
			IF @RUNSHEET_CODE = 'SPS'
				BEGIN
					--更新SPS明细实收数量
					UPDATE B
					SET B.[ACTUAL_INBOUND_PACKAGE] = D.[ACTUAL_BOX_NUM],
						B.[ACTUAL_INBOUND_PACKAGE_QTY] = D.[ACTUAL_QTY]
					FROM [LES].[TT_SPS_RUNSHEET] A WITH (NOLOCK)
					INNER JOIN [LES].[TT_SPS_RUNSHEET_DETAIL] B WITH (ROWLOCK) ON A.[SPS_RUNSHEET_SN] = B.[SPS_RUNSHEET_SN]
					INNER JOIN [LES].[TT_WMM_OUTPUT] C WITH (NOLOCK) ON C.[OUTPUT_NO] = A.[SPS_RUNSHEET_NO]
					INNER JOIN [LES].[TT_WMM_OUTPUT_DETAIL] D WITH (NOLOCK) ON D.[OUTPUT_ID] = C.[OUTPUT_ID] AND D.[PART_NO] = B.[PART_NO]
					WHERE C.[OUTPUT_ID] = @OUTPUT_ID

					--更新SPS拉动单状态
					UPDATE [LES].[TT_SPS_RUNSHEET] WITH (ROWLOCK)
					SET [ACTUAL_ARRIVAL_TIME] = GETDATE(),
						[SHEET_STATUS] = 1,
						[UPDATE_DATE] = GETDATE(),
						[UPDATE_USER] = @LOGINAME
					WHERE [SPS_RUNSHEET_NO] IN (SELECT [OUTPUT_NO] FROM [LES].[TT_WMM_OUTPUT] WITH (NOLOCK) WHERE [OUTPUT_ID] = @OUTPUT_ID)
				END
        COMMIT TRANSACTION
    END TRY
    BEGIN CATCH
        --出错，则返回执行不成功，回滚事务
        ROLLBACK TRANSACTION
        --记录错误信息
        INSERT INTO [LES].[TS_SYS_EXCEPTION] ([TIME_STAMP], [APPLICATION], [METHOD], [CLASS], [EXCEPTION_MESSAGE], [ERROR_CODE])
        SELECT GETDATE(), 'INTERFACE', '[LES].[PROC_TWD_UPDATE_OUTPUT_ISCLOSE]', 'Procedure', ERROR_MESSAGE(), ERROR_LINE()
    END CATCH
END