/********************************************************************/
/*   Project Name:  Production Pull System                          */
/*   Program Name:  [LES].[PROC_BAS_INHOUSE_PULL_COMBINE]	        */
/*   Called By:     by the windows service							*/
/*   Purpose:       更新计数器								        */
/*   author:        孙述霄	2017-05-24		   						*/
/********************************************************************/
CREATE PROCEDURE [LES].[PROC_BAS_INHOUSE_PULL_COMBINE]
AS
BEGIN
	DECLARE @rowcount INT

	BEGIN TRY
		UPDATE [LES].[TS_SYS_COMBINE_CONTROL] WITH (ROWLOCK) SET [ISPASS] = 1,[LAST_UPDATE_DATE] = GETDATE() WHERE [SEQID] = 1
		WAITFOR DELAY '00:00:20'
	END TRY
	BEGIN CATCH
		INSERT INTO [LES].[TS_SYS_EXCEPTION]([TIME_STAMP], [APPLICATION], [METHOD], [CLASS], [EXCEPTION_MESSAGE], [ERROR_CODE])
		SELECT  GETDATE(), 'INHOUSECOMBINE', '[PROC_BAS_INHOUSE_PULL_COMBINE1]', 'Procedure', ERROR_MESSAGE(), ERROR_LINE()
		RETURN -1
	END CATCH   

	BEGIN TRY
		BEGIN TRANSACTION
		
		SELECT @rowcount = COUNT(1) FROM [LES].[TE_BAS_INHOUSE_PULL_LOGISTIC_SUBMIT] WITH (NOLOCK)

		--更新PCS计数器
		IF @rowcount > 0
			BEGIN
				TRUNCATE TABLE [LES].[TM_BAS_INHOUSE_PULL_LOGISTIC_STANDARD]

				INSERT INTO [LES].[TM_BAS_INHOUSE_PULL_LOGISTIC_STANDARD]
				(
					[PLANT],
					[ASSEMBLY_LINE],
					[PLANT_ZONE],
					[WORKSHOP],
					[SUPPLIER_NUM],
					[TRANS_SUPPLIER_NUM],
					[MODEL],
					[PART_NO],
					[INDENTIFY_PART_NO],
					[AMOUNTRATIO],
					[EXTERIOR_COLOR],
					[INTERNAL_COLOR],
					[HAND_KEPT_RECORD],
					[COLOR_CONTROL_PATCH],
					[VWS],
					[RAND],
					[SECTION],
					[PART_OPTION],
					[PRODUCTION_NUMBER],
					[START_PRODUCTION_DATE],
					[CANCEL_NUMBER],
					[END_PRODUCTION_DATE],
					[MODEL_YEAR],
					[DOSAGE],
					[MEASURING_UNIT_NO],
					[AMOUNT_FLAG],
					[DATA_DATE],
					[VORSERIE],
					[PART_CNAME],
					[PART_ENAME],
					[PART_NICKNAME],
					[LOAD_FLAG],
					[ZP_FLAG],
					[REQUIREMENT_FLAG],
					[ASSEMBLY_FLAG],
					[ASSEMBLY_FLAG_RECRUIT],
					[PURCHASE_STYLE],
					[VALID_FLAG],
					[WORKSHOP_SECTION],
					[LOCATION],
					[IN_PLANT_LOGISTIC_MODE],
					[IN_PLANT_SYSTEM_MODE],
					[IN_PLANT_LOGISTIC_PART_CLASS],
					[INHOUSE_MODE],
					[INHOUSE_SYSTEM_MODE],
					[EFFECTIVE_STATUS],
					[START_EFFECTIVE_DATE],
					[INHOUSE_PART_CLASS],
					[DOCK],
					[INHOUSE_PACKAGE_MODEL],
					[INHOUSE_PACKAGE],
					[TERMAL_PK],
					[COMMENTS],
					[UPDATE_DATE],
					[UPDATE_USER],
					[CREATE_DATE],
					[CREATE_USER],
					[LOGICAL_PK],
					[BUSINESS_PK]
				)
				SELECT
					[PLANT],
					[ASSEMBLY_LINE],
					[PLANT_ZONE],
					[WORKSHOP],
					[SUPPLIER_NUM],
					[TRANS_SUPPLIER_NUM],
					[MODEL],
					[PART_NO],
					[INDENTIFY_PART_NO],
					[AMOUNTRATIO],
					[EXTERIOR_COLOR],
					[INTERNAL_COLOR],
					[HAND_KEPT_RECORD],
					[COLOR_CONTROL_PATCH],
					[VWS],
					[RAND],
					[SECTION],
					[PART_OPTION],
					[PRODUCTION_NUMBER],
					[START_PRODUCTION_DATE],
					[CANCEL_NUMBER],
					[END_PRODUCTION_DATE],
					[MODEL_YEAR],
					[DOSAGE],
					[MEASURING_UNIT_NO],
					[AMOUNT_FLAG],
					[DATA_DATE],
					[VORSERIE],
					[PART_CNAME],
					[PART_ENAME],
					[PART_NICKNAME],
					[LOAD_FLAG],
					[ZP_FLAG],
					[REQUIREMENT_FLAG],
					[ASSEMBLY_FLAG],
					[ASSEMBLY_FLAG_RECRUIT],
					[PURCHASE_STYLE],
					[VALID_FLAG],
					[WORKSHOP_SECTION],
					[LOCATION],
					[IN_PLANT_LOGISTIC_MODE],
					[IN_PLANT_SYSTEM_MODE],
					[IN_PLANT_LOGISTIC_PART_CLASS],
					[INHOUSE_MODE],
					[INHOUSE_SYSTEM_MODE],
					[EFFECTIVE_STATUS],
					[START_EFFECTIVE_DATE],
					[INHOUSE_PART_CLASS],
					[DOCK],
					[INHOUSE_PACKAGE_MODEL],
					[INHOUSE_PACKAGE],
					[TERMAL_PK],
					[COMMENTS],
					[UPDATE_DATE],
					[UPDATE_USER],
					[CREATE_DATE],
					[CREATE_USER],
					[LOGICAL_PK],
					[BUSINESS_PK]
				FROM [LES].[TE_BAS_INHOUSE_PULL_LOGISTIC_SUBMIT] WITH (NOLOCK)

				--插入到PCS计数器
				INSERT INTO [LES].[TT_PCS_COUNTER]
				(
					[PLANT],						--*关键
					[ASSEMBLY_LINE],				--*关键
					[SUPPLIER_NUM],					--*关键
					[PART_NO],						--*关键
					[INDENTIFY_PART_NO],
					[AMOUNTRATIO],
					[MEASURING_UNIT_NO],
					[PART_CNAME],
					[PART_ENAME],
					[LOCATION],						--*关键
					[IN_PLANT_LOGISTIC_PART_CLASS],	--*关键
					[INHOUSE_PACKAGE_MODEL],
					[INHOUSE_PACKAGE],
					[CURRENT_PART_COUNT],
					[CREATE_DATE]
				)         
				SELECT  
					A.[PLANT],
					A.[ASSEMBLY_LINE],
					C.[SUPPLIER_NUM],
					A.[PART_NO],
					A.[INDENTIFY_PART_NO],
					MIN(A.[AMOUNTRATIO]),
					MIN(A.[MEASURING_UNIT_NO]),
					MIN(A.[PART_CNAME]),
					MIN(A.[PART_ENAME]),
					A.[LOCATION],
					A.[IN_PLANT_LOGISTIC_PART_CLASS],
					MIN(A.[INHOUSE_PACKAGE_MODEL]),
					MIN(A.[INHOUSE_PACKAGE]),
					MIN(A.[INHOUSE_PACKAGE]),
					GETDATE()
				FROM [LES].[TM_BAS_INHOUSE_PULL_LOGISTIC_STANDARD] A WITH (NOLOCK)
				INNER JOIN [LES].[TM_PCS_ROUTE_BOX_PARTS] C WITH (NOLOCK) ON A.[PLANT] = C.[PLANT] AND A.[ASSEMBLY_LINE] = C.[ASSEMBLY_LINE] AND A.[IN_PLANT_LOGISTIC_PART_CLASS] = C.[BOX_PARTS]
				WHERE NOT EXISTS
				(
					SELECT
						*
					FROM [LES].[TT_PCS_COUNTER] B WITH (NOLOCK)
					WHERE A.[PLANT] = B.[PLANT] AND A.[ASSEMBLY_LINE] = B.[ASSEMBLY_LINE] AND C.[SUPPLIER_NUM] = B.[SUPPLIER_NUM]
					AND A.[LOCATION] = B.[LOCATION] AND A.[PART_NO] = B.[PART_NO] AND A.[IN_PLANT_LOGISTIC_PART_CLASS] = B.[IN_PLANT_LOGISTIC_PART_CLASS]
				) AND A.[IN_PLANT_SYSTEM_MODE] = 'PCS' 
				GROUP BY A.[PLANT], A.[ASSEMBLY_LINE], C.[SUPPLIER_NUM], A.[PART_NO], A.[INDENTIFY_PART_NO], A.[LOCATION], A.[IN_PLANT_LOGISTIC_PART_CLASS]

				--更新计数器--包装更改
				UPDATE A
				SET A.[INHOUSE_PACKAGE] = INHOUSE_PULL_GROUP.[INHOUSE_PACKAGE]
				FROM [LES].[TT_PCS_COUNTER] A WITH (ROWLOCK)
				INNER JOIN
				(
					SELECT
						[PLANT],
						[ASSEMBLY_LINE],
						[LOCATION],
						[PART_NO],
						[IN_PLANT_LOGISTIC_PART_CLASS],
						MIN([INHOUSE_PACKAGE_MODEL]) AS [INHOUSE_PACKAGE_MODEL],
						MIN([INHOUSE_PACKAGE]) AS [INHOUSE_PACKAGE]
					FROM [LES].[TM_BAS_INHOUSE_PULL_LOGISTIC_STANDARD] WITH (NOLOCK)
					WHERE [IN_PLANT_SYSTEM_MODE] = 'PCS'
					GROUP BY [PLANT], [ASSEMBLY_LINE], [LOCATION], [PART_NO], [IN_PLANT_LOGISTIC_PART_CLASS]
				) AS INHOUSE_PULL_GROUP
				ON A.[PLANT] = INHOUSE_PULL_GROUP.[PLANT] AND A.[ASSEMBLY_LINE] = INHOUSE_PULL_GROUP.[ASSEMBLY_LINE]
				AND A.[LOCATION] = INHOUSE_PULL_GROUP.[LOCATION] AND A.[PART_NO] = INHOUSE_PULL_GROUP.[PART_NO]
				AND A.[IN_PLANT_LOGISTIC_PART_CLASS] = INHOUSE_PULL_GROUP.[IN_PLANT_LOGISTIC_PART_CLASS]
				WHERE A.[INHOUSE_PACKAGE] != INHOUSE_PULL_GROUP.[INHOUSE_PACKAGE]

				--更新计数器----包装型号更改	
				UPDATE A SET
				A.[INHOUSE_PACKAGE_MODEL] = INHOUSE_PULL_GROUP.[INHOUSE_PACKAGE_MODEL]
				FROM [LES].[TT_PCS_COUNTER] A WITH (ROWLOCK)
				INNER JOIN
				(
					SELECT
						[PLANT],
						[ASSEMBLY_LINE],
						[LOCATION],
						[PART_NO],
						[IN_PLANT_LOGISTIC_PART_CLASS],
						MIN([INHOUSE_PACKAGE_MODEL])AS [INHOUSE_PACKAGE_MODEL],
						MIN([INHOUSE_PACKAGE]) AS [INHOUSE_PACKAGE]
					FROM [LES].[TM_BAS_INHOUSE_PULL_LOGISTIC_STANDARD] WITH (NOLOCK)
					WHERE [IN_PLANT_SYSTEM_MODE]='PCS'
					GROUP BY [PLANT], [ASSEMBLY_LINE], [LOCATION], [PART_NO], [IN_PLANT_LOGISTIC_PART_CLASS]
				) AS INHOUSE_PULL_GROUP
				ON A.[PLANT] = INHOUSE_PULL_GROUP.[PLANT] AND A.[ASSEMBLY_LINE] = INHOUSE_PULL_GROUP.[ASSEMBLY_LINE]
				AND A.[LOCATION] = INHOUSE_PULL_GROUP.[LOCATION] AND A.[PART_NO] = INHOUSE_PULL_GROUP.[PART_NO]
				AND A.[IN_PLANT_LOGISTIC_PART_CLASS] = INHOUSE_PULL_GROUP.[IN_PLANT_LOGISTIC_PART_CLASS]
				WHERE A.[INHOUSE_PACKAGE_MODEL] != INHOUSE_PULL_GROUP.[INHOUSE_PACKAGE_MODEL]
		    
				--删除计数器数据
				DELETE FROM [LES].[TT_PCS_COUNTER] WITH (ROWLOCK)
				WHERE NOT EXISTS
				(
					SELECT * FROM [LES].[TM_BAS_INHOUSE_PULL_LOGISTIC_STANDARD] B WITH (NOLOCK)
					WHERE [LES].[TT_PCS_COUNTER].[PLANT] = B.[PLANT] AND [LES].[TT_PCS_COUNTER].[ASSEMBLY_LINE] = B.[ASSEMBLY_LINE]
					AND [LES].[TT_PCS_COUNTER].[LOCATION] = B.[LOCATION] AND [LES].[TT_PCS_COUNTER].[PART_NO] = B.[PART_NO]
					AND [LES].[TT_PCS_COUNTER].[IN_PLANT_LOGISTIC_PART_CLASS] = B.[IN_PLANT_LOGISTIC_PART_CLASS]
					AND  B.[IN_PLANT_SYSTEM_MODE]='PCS'
				)  

				--删除计数器数据,由于供应商变更导致的无法删除的数据
				DELETE FROM [LES].[TT_PCS_COUNTER] WITH (ROWLOCK)
				WHERE NOT EXISTS
				(
					SELECT * FROM [LES].[TM_PCS_ROUTE_BOX_PARTS] B WITH (NOLOCK)
					WHERE [LES].[TT_PCS_COUNTER].[PLANT] = B.[PLANT] AND [LES].[TT_PCS_COUNTER].[ASSEMBLY_LINE] = B.[ASSEMBLY_LINE]
					AND [LES].[TT_PCS_COUNTER].[IN_PLANT_LOGISTIC_PART_CLASS] = B.[BOX_PARTS]
					AND [LES].[TT_PCS_COUNTER].[SUPPLIER_NUM] = B.[SUPPLIER_NUM]
				)
			END

			SELECT @rowcount = COUNT(1) FROM [LES].[TE_BAS_INBOUND_PULL_LOGISTIC_SUBMIT] WITH (NOLOCK)

			--更新TWD计数器
			IF @rowcount > 0
				BEGIN
					TRUNCATE TABLE [LES].[TM_BAS_INBOUND_PULL_LOGISTIC_STANDARD]

					INSERT INTO [LES].[TM_BAS_INBOUND_PULL_LOGISTIC_STANDARD]
					(
						[PLANT],
						[ASSEMBLY_LINE],
						[PLANT_ZONE],
						[WORKSHOP],
						[SUPPLIER_NUM],
						[TRANS_SUPPLIER_NUM],
						[MODEL],
						[PART_NO],
						[INDENTIFY_PART_NO],
						[AMOUNTRATIO],
						[EXTERIOR_COLOR],
						[INTERNAL_COLOR],
						[HAND_KEPT_RECORD],
						[COLOR_CONTROL_PATCH],
						[VWS],
						[RAND],
						[SECTION],
						[PART_OPTION],
						[PRODUCTION_NUMBER],
						[START_PRODUCTION_DATE],
						[CANCEL_NUMBER],
						[END_PRODUCTION_DATE],
						[MODEL_YEAR],
						[DOSAGE],
						[MEASURING_UNIT_NO],
						[AMOUNT_FLAG],
						[DATA_DATE],
						[VORSERIE],
						[PART_CNAME],
						[PART_ENAME],
						[LOAD_FLAG],
						[ZP_FLAG],
						[REQUIREMENT_FLAG],
						[LOGICAL_PK],
						[ASSEMBLY_FLAG],
						[ASSEMBLY_FLAG_RECRUIT],
						[MARK],
						[PURCHASE_STYLE],
						[VALID_FLAG],
						[INBOUND_MODE],
						[INBOUND_LOGISTIC_MODE],
						[INBOUND_SYSTEM_MODE],
						[EFFECTIVE_STATUS],
						[START_EFFECTIVE_DATE],
						[INBOUND_PART_CLASS],
						[DOCK],
						[INBOUND_PACKAGE_MODEL],
						[INBOUND_PACKAGE],
						[IS_SPLIT],
						[TERMAL_PK],
						[DIFF_FLAG],
						[COMMENTS],
						[UPDATE_DATE],
						[UPDATE_USER],
						[CREATE_DATE],
						[CREATE_USER],
						[MIN],
						[MAX],
						[WAREHOUSE],
						[S_WM_NO],
						[S_ZONE_NO],
						[MIN_PULL_BOX],
						[BATCH_PULL_BOX]
					)
					SELECT
						[PLANT],
						[ASSEMBLY_LINE],
						[PLANT_ZONE],
						[WORKSHOP],
						[SUPPLIER_NUM],
						[TRANS_SUPPLIER_NUM],
						[MODEL],
						[PART_NO],
						[INDENTIFY_PART_NO],
						[AMOUNTRATIO],
						[EXTERIOR_COLOR],
						[INTERNAL_COLOR],
						[HAND_KEPT_RECORD],
						[COLOR_CONTROL_PATCH],
						[VWS],
						[RAND],
						[SECTION],
						[PART_OPTION],
						[PRODUCTION_NUMBER],
						[START_PRODUCTION_DATE],
						[CANCEL_NUMBER],
						[END_PRODUCTION_DATE],
						[MODEL_YEAR],
						[DOSAGE],
						[MEASURING_UNIT_NO],
						[AMOUNT_FLAG],
						[DATA_DATE],
						[VORSERIE],
						[PART_CNAME],
						[PART_ENAME],
						[LOAD_FLAG],
						[ZP_FLAG],
						[REQUIREMENT_FLAG],
						[LOGICAL_PK],
						[ASSEMBLY_FLAG],
						[ASSEMBLY_FLAG_RECRUIT],
						[MARK],
						[PURCHASE_STYLE],
						[VALID_FLAG],
						[INBOUND_MODE],
						[INBOUND_LOGISTIC_MODE],
						[INBOUND_SYSTEM_MODE],
						[EFFECTIVE_STATUS],
						[START_EFFECTIVE_DATE],
						[INBOUND_PART_CLASS],
						[DOCK],
						[INBOUND_PACKAGE_MODEL],
						[INBOUND_PACKAGE],
						[IS_SPLIT],
						[TERMAL_PK],
						[DIFF_FLAG],
						[COMMENTS],
						[UPDATE_DATE],
						[UPDATE_USER],
						[CREATE_DATE],
						[CREATE_USER],
						[MIN],
						[MAX],
						[WAREHOUSE],
						[S_WM_NO],
						[S_ZONE_NO],
						[MIN_PULL_BOX],
						[BATCH_PULL_BOX]
					FROM [LES].[TE_BAS_INBOUND_PULL_LOGISTIC_SUBMIT] WITH (NOLOCK)

					INSERT INTO [LES].[TT_TWD_CONSUME_COUNTER]
					(
						[PLANT],					--*关键
						[ASSEMBLY_LINE],			--*关键
						[SUPPLIER_NUM],				--*关键
						[MODEL],					--*关键
						[PART_NO],					--*关键
						[INDENTIFY_PART_NO],
						[AMOUNTRATIO],
						[INBOUND_PART_CLASS],
						[MEASURING_UNIT_NO],
						[PART_CNAME],
						[PART_ENAME],
						[DOCK],						--*关键
						[INBOUND_PACKAGE_MODEL],
						[INBOUND_PACKAGE],
						[CURRENT_PART_COUNT],
						[CREATE_DATE],
						[INHOUSE_PACKAGE_MODEL],
						[PULL_MODE]					--*关键
					)
					SELECT DISTINCT
						A.[PLANT],
						A.[ASSEMBLY_LINE],
						A.[SUPPLIER_NUM],
						MIN(A.[MODEL]),
						A.[PART_NO],
						MIN(A.[INDENTIFY_PART_NO]),
						MIN(A.[AMOUNTRATIO]),
						A.[INBOUND_PART_CLASS],
						MIN(A.[MEASURING_UNIT_NO]),
						MIN(A.[PART_CNAME]),
						MIN(A.[PART_ENAME]),
						MIN(A.[DOCK]),
						MIN(A.[INBOUND_PACKAGE_MODEL]),
						MIN(A.[INBOUND_PACKAGE]),
						0,
						GETDATE(),
						MIN(A.MARK),
						MIN(C.[PULL_MODE])
					FROM [LES].[TM_BAS_INBOUND_PULL_LOGISTIC_STANDARD] A WITH (NOLOCK)
					INNER JOIN [LES].[TM_TWD_BOX_PARTS] C WITH (NOLOCK)
					ON C.[BOX_PARTS] = A.[INBOUND_PART_CLASS] AND C.[PLANT] = A.[PLANT]
					AND C.[ASSEMBLY_LINE] = A.[ASSEMBLY_LINE] AND C.[SUPPLIER_NUM] = A.[SUPPLIER_NUM]
					WHERE NOT EXISTS
					(
						SELECT * FROM [LES].[TT_TWD_CONSUME_COUNTER] B WITH (NOLOCK)
						WHERE A.[PLANT] = B.[PLANT] AND A.[ASSEMBLY_LINE] = B.[ASSEMBLY_LINE] AND A.[SUPPLIER_NUM] = B.[SUPPLIER_NUM]
						AND A.[INBOUND_PART_CLASS] = B.[INBOUND_PART_CLASS] AND A.[PART_NO] = B.[PART_NO]
					) AND A.[INBOUND_SYSTEM_MODE] = 'TWD'
					GROUP BY A.[PLANT], A.[ASSEMBLY_LINE], A.[SUPPLIER_NUM], A.[PART_NO], A.[INBOUND_PART_CLASS]		  

					--更新计数器
					UPDATE A
					SET A.[INBOUND_PACKAGE] = B.[INBOUND_PACKAGE],
					A.[PALLET_COUNT] = B.[INBOUND_PACKAGE] * A.[PALLET_PACKAGE]
					FROM [LES].[TT_TWD_CONSUME_COUNTER] A WITH (ROWLOCK)
					INNER JOIN [LES].[TM_BAS_INBOUND_PULL_LOGISTIC_STANDARD] B WITH (NOLOCK)
					ON A.[PLANT] = B.[PLANT] AND A.[ASSEMBLY_LINE] = B.[ASSEMBLY_LINE]
					AND A.[SUPPLIER_NUM] = B.[SUPPLIER_NUM] AND A.[PART_NO]=B.[PART_NO]
					AND A.[INBOUND_PART_CLASS]=B.[INBOUND_PART_CLASS] AND B.[INBOUND_SYSTEM_MODE] = 'TWD'
					WHERE A.[INBOUND_PACKAGE] != B.[INBOUND_PACKAGE]

					UPDATE A
					SET A.[INBOUND_PACKAGE_MODEL] = B.[INBOUND_PACKAGE_MODEL]
					FROM [LES].[TT_TWD_CONSUME_COUNTER] A WITH (ROWLOCK)
					INNER JOIN [LES].[TM_BAS_INBOUND_PULL_LOGISTIC_STANDARD] B WITH (NOLOCK)
					ON A.[PLANT] = B.[PLANT] AND A.[ASSEMBLY_LINE] = B.[ASSEMBLY_LINE]
					AND A.[SUPPLIER_NUM] = B.[SUPPLIER_NUM] AND A.[PART_NO] = B.[PART_NO]
					AND A.[INBOUND_PART_CLASS] = B.[INBOUND_PART_CLASS] AND B.[INBOUND_SYSTEM_MODE] = 'TWD'
					WHERE A.[INBOUND_PACKAGE_MODEL] != B.[INBOUND_PACKAGE_MODEL]
		
					--更新计数器--用量比例，中英文名称变更
					UPDATE A
					SET A.[AMOUNTRATIO] = B.[AMOUNTRATIO],
						A.[PART_CNAME] = B.[PART_CNAME],
						A.[PART_ENAME] = B.[PART_ENAME]
					FROM [LES].[TT_TWD_CONSUME_COUNTER] A WITH (ROWLOCK)
					INNER JOIN [LES].[TM_BAS_INBOUND_PULL_LOGISTIC_STANDARD] B WITH (NOLOCK)
					ON A.[PLANT] = B.[PLANT] 
					AND A.[ASSEMBLY_LINE] = B.[ASSEMBLY_LINE] 
					AND A.[SUPPLIER_NUM] = B.[SUPPLIER_NUM] 
					AND A.[INBOUND_PART_CLASS] = B.[INBOUND_PART_CLASS] 
					AND A.[PART_NO] = B.[PART_NO]
					AND B.[INBOUND_SYSTEM_MODE] = 'TWD'
					WHERE A.AMOUNTRATIO != B.AMOUNTRATIO OR A.PART_CNAME != B.PART_CNAME OR A.PART_ENAME != B.PART_ENAME
	   
					--更新组单零件类 
					UPDATE A SET
					A.[INHOUSE_PACKAGE_MODEL] = B.[MARK]
					FROM [LES].[TT_TWD_CONSUME_COUNTER] A WITH (ROWLOCK)
					INNER JOIN [LES].[TM_BAS_INBOUND_PULL_LOGISTIC_STANDARD] B WITH (NOLOCK)
					ON A.[PLANT] = B.[PLANT] AND A.[ASSEMBLY_LINE] = B.[ASSEMBLY_LINE]
					AND A.[SUPPLIER_NUM] = B.[SUPPLIER_NUM] AND A.[PART_NO]=B.[PART_NO]
					AND A.[INBOUND_PART_CLASS] = B.[INBOUND_PART_CLASS] AND B.[INBOUND_SYSTEM_MODE] = 'TWD'
					WHERE  A.[INHOUSE_PACKAGE_MODEL] != B.[MARK]

					--更新拉动模式不同
					UPDATE A
					SET A.[PULL_MODE] = C.[PULL_MODE]
					FROM [LES].[TT_TWD_CONSUME_COUNTER] A WITH (ROWLOCK)
					INNER JOIN [LES].[TM_TWD_BOX_PARTS] C WITH (NOLOCK)
					ON C.[BOX_PARTS] = A.[INBOUND_PART_CLASS] AND C.[PLANT] = A.[PLANT]
					AND C.[ASSEMBLY_LINE] = A.[ASSEMBLY_LINE] AND C.[SUPPLIER_NUM] = A.[SUPPLIER_NUM]
					WHERE ISNULL(A.[PULL_MODE], '') != ISNULL(C.[PULL_MODE], '')
		
					--更新MIN/MAX，最小拉动箱数、批量拉动箱数
					UPDATE A
					SET A.[MIN] = B.[MIN],
						A.[MAX] = B.[MAX],
						A.[MIN_PULL_BOX] = B.[MIN_PULL_BOX],
						A.[BATCH_PULL_BOX] = B.[BATCH_PULL_BOX]
					FROM [LES].[TT_TWD_CONSUME_COUNTER] A WITH (ROWLOCK)
					INNER JOIN [LES].[TM_BAS_INBOUND_PULL_LOGISTIC_STANDARD] B WITH (NOLOCK)
					ON A.[PLANT] = B.[PLANT] AND A.[ASSEMBLY_LINE] = B.[ASSEMBLY_LINE]
					AND A.[SUPPLIER_NUM] = B.[SUPPLIER_NUM] AND A.[PART_NO]=B.[PART_NO]
					AND A.[INBOUND_PART_CLASS] = B.[INBOUND_PART_CLASS] AND B.[INBOUND_SYSTEM_MODE] = 'TWD'
					WHERE ISNULL(A.[MIN], 0) != ISNULL(B.[MIN], 0) OR ISNULL(A.[MAX], 0) != ISNULL(B.[MAX], 0) OR ISNULL(A.[MIN_PULL_BOX], 0) != ISNULL(B.[MIN_PULL_BOX], 0) OR ISNULL(A.[BATCH_PULL_BOX], 0) != ISNULL(B.[BATCH_PULL_BOX], 0)

					--删除计数器数据
					DELETE FROM [LES].[TT_TWD_CONSUME_COUNTER] WITH (ROWLOCK)
					WHERE NOT EXISTS
					(
						SELECT * FROM [LES].[TM_BAS_INBOUND_PULL_LOGISTIC_STANDARD] B WITH (NOLOCK)
						WHERE [LES].[TT_TWD_CONSUME_COUNTER].[PLANT] = B.[PLANT]
						AND [LES].[TT_TWD_CONSUME_COUNTER].[ASSEMBLY_LINE] = B.[ASSEMBLY_LINE]
						AND [LES].[TT_TWD_CONSUME_COUNTER].[SUPPLIER_NUM] = B.[SUPPLIER_NUM]
						AND [LES].[TT_TWD_CONSUME_COUNTER].[INBOUND_PART_CLASS] = B.[INBOUND_PART_CLASS]
						AND [LES].[TT_TWD_CONSUME_COUNTER].[PART_NO] = B.[PART_NO]
						AND B.[INBOUND_SYSTEM_MODE]='TWD'
					)
				END  

			UPDATE [LES].[TS_SYS_COMBINE_CONTROL] WITH (ROWLOCK) SET [ISPASS] = 0 WHERE [SEQID] = 1
		COMMIT TRANSACTION

		RETURN  0 --成功
	END TRY
	BEGIN CATCH
		--出错，则返回执行不成功，回滚事务
		ROLLBACK TRANSACTION
		UPDATE [LES].[TS_SYS_COMBINE_CONTROL] WITH (ROWLOCK) SET [ISPASS] = 0 WHERE [SEQID] = 1
		--记录错误信息
		INSERT INTO [LES].[TS_SYS_EXCEPTION] ([TIME_STAMP], [APPLICATION], [METHOD], [CLASS], [EXCEPTION_MESSAGE], [ERROR_CODE])
		SELECT GETDATE(), 'INHOUSECOMBINE', '[PROC_BAS_INHOUSE_PULL_COMBINE2]', 'Procedure', ERROR_MESSAGE(), ERROR_LINE()
		RETURN -1
	END CATCH
END