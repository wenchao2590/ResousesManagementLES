/********************************************************************/
/*   Project Name:  LES System										*/
/*   Program Name:  [PROC_TT_WMM_OUTPUT_DETAIL_CREATE_UPDATE]       */
/*   Called By:     by the Page										*/
/*   Purpose:       This is the main stored procedure for the       */
/*   author:       andy	2015-06-09     								*/
/********************************************************************/
CREATE PROCEDURE [LES].[PROC_WMM_OUTPUT_DETAIL_CREATE_UPDATE]
AS
BEGIN
	INSERT INTO [LES].[TT_WMM_OUTPUT_DETAIL]
	(
		[OUTPUT_ID],
		[TRAN_NO],
		[PLANT],
		[WM_NO],
		[ZONE_NO],
		[DLOC],
		[PART_NO],
		[PART_CNAME],
		[REQUIRED_BOX_NUM],
		[REQUIRED_QTY],
		[ACTUAL_BOX_NUM],
		[ACTUAL_QTY],
		[SUPPLIER_NUM],
		[PACKAGE_MODEL],
		[PACKAGE],
		[PACK_COUNT],
		[TARGET_WM],
		[TARGET_ZONE],
		[TARGET_DLOC],
		[COMMENTS],
		[CREATE_USER],
		[CREATE_DATE],
		[UPDATE_USER],
		[UPDATE_DATE]
	)
	SELECT 
		TT.[OUTPUT_ID],
		TT.[OUTPUT_NO],
		TT.[PLANT],
		TT.[WM_NO],
		TT.[ZONE_NO],
		ST.[DLOC],
		TE.[PART_NO],
		ST.[PART_CNAME],
		TE.[REQUIRED_BOX_NUM],
		TE.[REQUIRED_QTY],
		TE.[ACTUAL_BOX_NUM],
		TE.[ACTUAL_QTY],
		TT.[SUPPLIER_NUM],
		ST.[INBOUND_PACKAGE_MODEL],
		ST.[INBOUND_PACKAGE],
		ST.[INBOUND_PACKAGE],
		TM_TARGET.[WM_NO],
		TM_TARGET.[ZONE_NO],
		TM_TARGET.[DLOC],
		TE.[COMMENTS],
		TE.[CREATE_USER],
		TE.[CREATE_DATE],
		TE.[UPDATE_USER],
		TE.[UPDATE_DATE]
	FROM [LES].[TE_WMM_OUTPUT_DETAIL_TEMP] TE WITH (NOLOCK)
	INNER JOIN [LES].[TT_WMM_OUTPUT] TT WITH (NOLOCK) ON TE.[VALID_FLAG] = 1 AND (TT.[CONFIRM_FLAG] = 0 OR TT.[CONFIRM_FLAG] IS NULL) AND TE.[OUTPUT_NO] = TT.[OUTPUT_NO]
	INNER JOIN [LES].[TM_BAS_PARTS_STOCK] ST WITH (NOLOCK) ON TT.[PLANT] = ST.[PLANT] AND TT.[WM_NO] = ST.[WM_NO] AND TT.[ZONE_NO] = ST.[ZONE_NO] AND TE.[PART_NO] = ST.[PART_NO]
	INNER JOIN [LES].[TM_WMM_ZONES] Z WITH (NOLOCK) ON Z.[ZONE_NO] = TT.[PLANT_ZONE]
	INNER JOIN [LES].[TM_BAS_PARTS_STOCK] TM_TARGET WITH (NOLOCK) ON TM_TARGET.[PART_NO] = TE.[PART_NO] AND TM_TARGET.[PLANT] = Z.[PLANT] AND TM_TARGET.[WM_NO] = Z.[WM_NO] AND TM_TARGET.[ZONE_NO] = Z.[ZONE_NO]
	WHERE (SELECT COUNT(*) FROM [LES].[TT_WMM_OUTPUT_DETAIL] t WITH (NOLOCK) WHERE t.[OUTPUT_ID] = TT.[OUTPUT_ID] AND t.[PART_NO] = TE.[PART_NO]) = 0

	UPDATE [LES].[TT_WMM_OUTPUT_DETAIL]
	SET
		[OUTPUT_ID] = TT.[OUTPUT_ID],
		[TRAN_NO] = TT.[OUTPUT_NO],
		[PLANT] = TT.[PLANT],
		[WM_NO] = TT.[WM_NO],
		[ZONE_NO] = TT.[ZONE_NO],
		[DLOC] = ST.DLOC,
		[PART_NO] = TE.[PART_NO],
		[PART_CNAME] = ST.[PART_CNAME],
		[REQUIRED_BOX_NUM] = TE.[REQUIRED_BOX_NUM],
		[REQUIRED_QTY] = TE.[REQUIRED_QTY],
		[ACTUAL_BOX_NUM] = TE.[ACTUAL_BOX_NUM],
		[ACTUAL_QTY] = TE.[ACTUAL_QTY],
		[SUPPLIER_NUM] = TT.[SUPPLIER_NUM],
		[PACKAGE_MODEL] = ST.[INBOUND_PACKAGE_MODEL],
		[PACKAGE] = ST.[INBOUND_PACKAGE],
		[PACK_COUNT] = ST.[INBOUND_PACKAGE],
		[TARGET_WM]= TM_TARGET.[WM_NO],
		[TARGET_ZONE]=TM_TARGET.[ZONE_NO],
		[TARGET_DLOC] = TM_TARGET.[DLOC],
		[COMMENTS] = TE.[COMMENTS],
		[UPDATE_USER] = TE.[CREATE_USER],
		[UPDATE_DATE] =TE.[CREATE_DATE]
	FROM [LES].[TE_WMM_OUTPUT_DETAIL_TEMP] TE WITH (NOLOCK)
	INNER JOIN [LES].[TT_WMM_OUTPUT] TT WITH (NOLOCK) ON TE.[VALID_FLAG] = 1 AND (TT.[CONFIRM_FLAG] = 0 OR TT.[CONFIRM_FLAG] IS NULL) AND TE.[OUTPUT_NO] = TT.[OUTPUT_NO]
	INNER JOIN [LES].[TT_WMM_OUTPUT_DETAIL] DE WITH (ROWLOCK) ON TT.[OUTPUT_ID] = DE.[OUTPUT_ID] AND TE.[PART_NO] = DE.[PART_NO]
	INNER JOIN [LES].[TM_BAS_PARTS_STOCK] ST WITH (NOLOCK) ON TT.[PLANT] = ST.[PLANT] AND TT.[WM_NO] = ST.[WM_NO] AND TT.[ZONE_NO] = ST.[ZONE_NO] AND TE.[PART_NO] = ST.[PART_NO]
	INNER JOIN [LES].[TM_WMM_ZONES] Z WITH (NOLOCK) ON Z.[ZONE_NO] = TT.[PLANT_ZONE]
	INNER JOIN [LES].[TM_BAS_PARTS_STOCK] TM_TARGET WITH (NOLOCK) ON TM_TARGET.[PART_NO] = TE.[PART_NO] AND TM_TARGET.[PLANT] = Z.[PLANT] AND TM_TARGET.[WM_NO] = Z.[WM_NO] AND TM_TARGET.[ZONE_NO] = Z.[ZONE_NO]
	
	DELETE FROM [LES].[TT_WMM_OUTPUT_DETAIL] WITH (ROWLOCK)
	WHERE [OUTPUT_DETAIL_ID] IN
	(
		SELECT 
			T2.OUTPUT_DETAIL_ID 
		FROM
		(
			SELECT 
				TT.[OUTPUT_ID], TE.[PART_NO], TT.[OUTPUT_NO]
			FROM [LES].[TT_WMM_OUTPUT] TT WITH (NOLOCK)
			JOIN [LES].[TE_WMM_OUTPUT_DETAIL_TEMP] TE WITH (NOLOCK)
			ON TE.[VALID_FLAG] = 1 AND TE.[OUTPUT_NO] = TT.[OUTPUT_NO]
			AND (TT.[CONFIRM_FLAG] = 0 OR TT.[CONFIRM_FLAG] IS NULL)
		) T1
		RIGHT JOIN
		(
			SELECT 
				TT.[OUTPUT_ID], DE.[PART_NO], DE.[OUTPUT_DETAIL_ID], TT.[OUTPUT_NO]
			FROM [LES].[TT_WMM_OUTPUT_DETAIL] DE WITH (NOLOCK)
			INNER JOIN [LES].[TT_WMM_OUTPUT] TT WITH (NOLOCK) ON TT.OUTPUT_ID = DE.OUTPUT_ID
			INNER JOIN [LES].[TE_WMM_OUTPUT_DETAIL_TEMP] TE WITH (NOLOCK) ON TE.[OUTPUT_NO] = TT.[OUTPUT_NO]
			WHERE (TT.[CONFIRM_FLAG] = 0 OR TT.[CONFIRM_FLAG] IS NULL)
		) T2
		ON T1.[OUTPUT_ID] = T2.[OUTPUT_ID] AND T1.[PART_NO] = T2.[PART_NO]
		WHERE T1.[PART_NO] IS NULL
	)
END