/****************************************************************/
/*   Project Name:  TWD											*/
/*   Program Name:  [LES].[PROC_SPM_CREATE_ASN_BY_CARMODEL]		*/
/*   Called By:     web page									*/
/*   Author:        孙述霄										*/
/*   Create date:	2017-09-15									*/
/*   Note:			根据车型订单创建ASN单						*/
/****************************************************************/
CREATE PROCEDURE [LES].[PROC_SPM_CREATE_ASN_BY_CARMODEL]
(
	@ASNNO NVARCHAR(50),					--正式ASN单号
	@ASN_NO NVARCHAR(50),					--临时ASN单号
	@PHONE NVARCHAR(25),					--电话
	@DRIVER NVARCHAR(16),					--司机
	@PLATE_NO NVARCHAR(16),					--车牌号
	@PROMISE_TIME NVARCHAR(50),				--承诺到达时间
	@LoginName NVARCHAR(50),				--创建人
    @Result INT OUTPUT,						--执行结果，0-成功，1-失败
	@ResultMessage NVARCHAR(4000) OUTPUT	--错误信息
)
AS
BEGIN
    BEGIN TRY
        BEGIN TRAN
			SET NOCOUNT ON

			DECLARE @ASNID BIGINT					--ASN单ID号
			DECLARE @TWD_RUNSHEET_NO NVARCHAR(50)	--拉动单号
			DECLARE @PLANT NVARCHAR(8)				--工厂
			DECLARE @ASSEMBLY_LINE NVARCHAR(16)		--流水线
			DECLARE @WORKSHOP NVARCHAR(4)			--车间
			DECLARE @PLANT_ZONE NVARCHAR(16)		--目标存储区
			DECLARE @SUPPLIER_NUM NVARCHAR(16)		--供应商
			DECLARE @DOCK NVARCHAR(12)				--道口
			DECLARE @DELIVERY_LOCATION NVARCHAR(64)	--目标仓库
			DECLARE @BOX_PARTS NVARCHAR(16)			--零件类
			DECLARE @PART_TYPE INT					--零件类型
			DECLARE @RUNSHEET_TYPE INT				--拉动单类型

			SET @Result = 0
			SET @ResultMessage = ''

			--获取拉动单号
			SELECT TOP 1 @TWD_RUNSHEET_NO = [TWD_RUNSHEET_NO] FROM [LES].[TL_TWD_MATERIAL_TRAY_LOG] WITH (NOLOCK) WHERE [IS_ASN] = 1 AND [PROCESS_FLAG] = 1 AND [IS_GENERATE] = 0 AND [ASN_NO] = @ASN_NO ORDER BY [ID] DESC
			--获取拉动单类型
			SELECT @RUNSHEET_TYPE = [RUNSHEET_TYPE] FROM [LES].[TT_TWD_RUNSHEET] WITH (NOLOCK) WHERE [TWD_RUNSHEET_NO] = @TWD_RUNSHEET_NO
			--获取拉动单信息
			SELECT
				@PLANT = [PLANT],
				@ASSEMBLY_LINE = [ASSEMBLY_LINE],
				@WORKSHOP = [WORKSHOP],
				@PLANT_ZONE = [PLANT_ZONE],
				@SUPPLIER_NUM = [SUPPLIER_NUM],
				@DOCK = [DOCK],
				@DELIVERY_LOCATION = [DELIVERY_LOCATION],
				@BOX_PARTS = [BOX_PARTS],
				@PART_TYPE = [PART_TYPE]
			FROM [LES].[TT_TWD_RUNSHEET] WITH (NOLOCK)
			WHERE [TWD_RUNSHEET_NO] = @TWD_RUNSHEET_NO

			--生成ASN单主表
			INSERT INTO [LES].[TT_SPM_RUNSHEET_ASN]
			(
				[ASN_NO],
				[PLANT],
				[ASSEMBLY_LINE],
				[WORKSHOP],
				[PLANT_ZONE],
				[RUNSHEET_TYPE],
				[SUPPLIER_NUM],
				[SUPPLIER_SN],
				[DOCK],
				[DELIVERY_LOCATION],
				[BOX_PARTS],
				[PART_TYPE],
				[PROMISE_DELIVERY_TIME],
				[STATUS],
				[CREATE_DATE],
				[CREATE_USER],
				[MODIFY_DATE],
				[MODIFY_USER],
				[VALID_FLAG],
				[PRINT_TIMES],
				[LAST_PRINT_TIME],
				[CARRIER_DRIVER],
				[CARRIER_TRUCK_LICENCE_PLATE_NO],
				[IS_URGENT],
				[CARRIER_PHONE]
			)
			VALUES
			(
				@ASNNO,
				@PLANT,
				@ASSEMBLY_LINE,
				@WORKSHOP,
				@PLANT_ZONE,
				3,
				@SUPPLIER_NUM,
				0,
				@DOCK,
				@DELIVERY_LOCATION,
				@BOX_PARTS,
				@PART_TYPE,
				@PROMISE_TIME,
				0,
				GETDATE(),
				@LoginName,
				NULL,
				'',
				1,
				0,
				NULL,
				@DRIVER,
				@PLATE_NO,
				(CASE WHEN @RUNSHEET_TYPE = 2 THEN 1 ELSE 0 END),
				@PHONE
			)
			SELECT @ASNID = SCOPE_IDENTITY()
			--生成ASN单明细表
			INSERT INTO [LES].[TT_SPM_RUNSHEET_ASN_DETAIL]
			(
				[ASN_ID],
				[ASN_NO],
				[TWD_RUNSHEET_NO],
				[TWD_RUNSHEET_SN],
				[RUNSHEET_DETAIL_ID],
				[PLANT],
				[ASSEMBLY_LINE],
				[SUPPLIER_NUM],
				[PART_NO],
				[IDENTIFY_PART_NO],
				[PART_CNAME],
				[PART_ENAME],
				[DOCK],
				[DELIVERY_LOCATION],
				[BOX_PARTS],
				[INBOUND_PACKAGE],
				[MEASURING_UNIT_NO],
				[INBOUND_PACKAGE_MODEL],
				[PACK_COUNT],
				[REQUIRED_INBOUND_PACKAGE],
				[REQUIRED_INBOUND_PACKAGE_QTY],
				[ACTUAL_INBOUND_PACKAGE],
				[ACTUAL_INBOUND_PACKAGE_QTY],
				[STATUS],
				[VALID_FLAG],
				[CREATE_DATE],
				[CREATE_USER],
				[MODIFY_DATE],
				[MODIFY_USER],
				[PRINT_STATE],
				[PRINT_SUPPLEMENT],
				[PRINT_TIMES]
			)
			SELECT
				@ASNID,
				@ASNNO,
				B.[TWD_RUNSHEET_NO],
				B.[TWD_RUNSHEET_SN],
				C.[RUNSHEET_DETAIL_ID],
				B.[PLANT],
				B.[ASSEMBLY_LINE],
				B.[SUPPLIER_NUM],
				C.[PART_NO],
				C.[IDENTIFY_PART_NO],
				C.[PART_CNAME],
				C.[PART_ENAME],
				B.[DOCK],
				B.[DELIVERY_LOCATION],
				B.[BOX_PARTS],
				C.[INBOUND_PACKAGE],
				'' AS [MEASURING_UNIT_NO],
				C.[INBOUND_PACKAGE_MODEL],
				CEILING(CAST(A.[PACK_COUNT] AS DECIMAL) / C.[INBOUND_PACKAGE]) AS [PACK_COUNT],
				CEILING(CAST(A.[PACK_COUNT] AS DECIMAL) / C.[INBOUND_PACKAGE]) AS [REQUIRED_INBOUND_PACKAGE],
				A.[PACK_COUNT] AS [REQUIRED_INBOUND_PACKAGE_QTY],
				0,
				0,
				0,
				1,
				GETDATE(),
				@LoginName,
				NULL,
				NULL,
				0,
				0,
				0
			FROM
			(
				SELECT
					A.[TWD_RUNSHEET_NO],
					A.[PLANT],
					A.[ASSEMBLY_LINE],
					A.[SUPPLIER_NUM],
					A.[BOX_PARTS],
					A.[PART_NO],
					A.[PART_CNAME],
					A.[PACK_COUNT] * ISNULL(B.[SET_NUM], 1) AS [PACK_COUNT]
				FROM
				(
					SELECT
						[TWD_RUNSHEET_NO],
						[PLANT],
						[ASSEMBLY_LINE],
						[SUPPLIER_NUM],
						[BOX_PARTS],
						[PART_NO],
						MAX([PART_CNAME]) AS [PART_CNAME],
						SUM([PACK_COUNT]) AS [PACK_COUNT]
					FROM [LES].[TL_TWD_MATERIAL_TRAY_LOG] WITH (NOLOCK)
					WHERE [IS_ASN] = 1 AND [PROCESS_FLAG] = 1 AND [IS_GENERATE] = 0 AND [ASN_NO] = @ASN_NO
					GROUP BY [TWD_RUNSHEET_NO], [PLANT], [ASSEMBLY_LINE], [SUPPLIER_NUM], [BOX_PARTS], [PART_NO]
				) AS A
				LEFT JOIN [LES].[TT_SPS_MANUAL_PULL] B WITH (NOLOCK) ON A.[TWD_RUNSHEET_NO] = B.[TWD_RUNSHEET_NO]
			) AS A
			INNER JOIN [LES].[TT_TWD_RUNSHEET] B WITH (NOLOCK) ON A.[TWD_RUNSHEET_NO] = B.[TWD_RUNSHEET_NO]
			INNER JOIN [LES].[TT_TWD_RUNSHEET_DETAIL] C WITH (NOLOCK) ON C.[TWD_RUNSHEET_SN] = B.[TWD_RUNSHEET_SN] AND C.[PART_NO] = A.[PART_NO]
			--更新TWD单据草稿数量
			UPDATE C
			SET C.[ASN_DRAFT_QTY] = ISNULL(C.[ASN_DRAFT_QTY], 0) + A.[PACK_COUNT]
			FROM
			(
				SELECT
					A.[TWD_RUNSHEET_NO],
					A.[PLANT],
					A.[ASSEMBLY_LINE],
					A.[SUPPLIER_NUM],
					A.[BOX_PARTS],
					A.[PART_NO],
					A.[PART_CNAME],
					A.[PACK_COUNT] * ISNULL(B.[SET_NUM], 1) AS [PACK_COUNT]
				FROM
				(
					SELECT
						[TWD_RUNSHEET_NO],
						[PLANT],
						[ASSEMBLY_LINE],
						[SUPPLIER_NUM],
						[BOX_PARTS],
						[PART_NO],
						MAX([PART_CNAME]) AS [PART_CNAME],
						SUM([PACK_COUNT]) AS [PACK_COUNT]
					FROM [LES].[TL_TWD_MATERIAL_TRAY_LOG] WITH (NOLOCK)
					WHERE [IS_ASN] = 1 AND [PROCESS_FLAG] = 1 AND [IS_GENERATE] = 0 AND [ASN_NO] = @ASN_NO
					GROUP BY [TWD_RUNSHEET_NO], [PLANT], [ASSEMBLY_LINE], [SUPPLIER_NUM], [BOX_PARTS], [PART_NO]
				) AS A
				LEFT JOIN [LES].[TT_SPS_MANUAL_PULL] B WITH (NOLOCK) ON A.[TWD_RUNSHEET_NO] = B.[TWD_RUNSHEET_NO]
			) AS A
			INNER JOIN [LES].[TT_TWD_RUNSHEET] B WITH (NOLOCK) ON A.[TWD_RUNSHEET_NO] = B.[TWD_RUNSHEET_NO]
			INNER JOIN [LES].[TT_TWD_RUNSHEET_DETAIL] C WITH (ROWLOCK) ON C.[TWD_RUNSHEET_SN] = B.[TWD_RUNSHEET_SN] AND C.[PART_NO] = A.[PART_NO]
			--更新正式ASN单号
			UPDATE [LES].[TL_TWD_MATERIAL_TRAY_LOG] WITH (ROWLOCK)
			SET [ASN_NO] = @ASNNO,
				[IS_GENERATE] = 2,
				[UPDATE_DATE] = GETDATE(),
				[UPDATE_USER] = @LoginName
			WHERE [IS_ASN] = 1 AND [PROCESS_FLAG] = 1 AND [IS_GENERATE] = 0 AND [ASN_NO] = @ASN_NO

			SET NOCOUNT OFF

		IF @Result = 0
			COMMIT TRAN
		ELSE
			ROLLBACK TRANSACTION
    END TRY
    BEGIN CATCH
        ROLLBACK TRANSACTION

        SET @Result = 1
		SET @ResultMessage = ERROR_MESSAGE()
		--记录错误信息
		INSERT INTO [LES].[TS_SYS_EXCEPTION] ([TIME_STAMP], [APPLICATION], [METHOD], [CLASS],  [EXCEPTION_MESSAGE], [ERROR_CODE])
		SELECT GETDATE(), 'INTERFACE', '[LES].[PROC_SPM_CREATE_ASN_BY_CARMODEL]', 'Procedure', @ResultMessage, ERROR_LINE()
    END CATCH
END