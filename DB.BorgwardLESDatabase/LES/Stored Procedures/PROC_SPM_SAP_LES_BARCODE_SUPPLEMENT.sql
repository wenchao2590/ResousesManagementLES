/********************************************************************/
/*   Program Name:  [LES].[PROC_SPM_SAP_LES_BARCODE_SUPPLEMENT]		*/
/*   Called By:     web page										*/
/*   Author:        孙述霄											*/
/*   Create date:	2017-09-04										*/
/*   Note:			交货单 生成补打条码								*/
/********************************************************************/
CREATE PROCEDURE [LES].[PROC_SPM_SAP_LES_BARCODE_SUPPLEMENT]
(
	@RunsheetDetailId INT,
	@PrintQty INT
)
AS
BEGIN
	DECLARE @PLAN_RUNSHEET_NO nvarchar(30)
	DECLARE @WM_NO nvarchar(10)
	DECLARE @ZONE_NO nvarchar(20)
	DECLARE @PLANT_ZONE NVARCHAR(20)
	DECLARE @SUPPLIER_NUM nvarchar(12)
	DECLARE @PART_NO nvarchar(20)
	DECLARE @PART_CNAME nvarchar(300)
	DECLARE @BOX_PARTS nvarchar(10)
	DECLARE @PICKUP_SEQ_NO INT
	DECLARE @DLOC varchar(20)--库位地址
	DECLARE @MEASURING_UNIT_NO nvarchar(1)
	DECLARE @INHOUSE_PACKAGE_MODEL nvarchar(30)
	DECLARE @PLANT nvarchar(5)
	DECLARE @DOCK nvarchar(10)
	DECLARE @INHOUSE_PACKAGE int
	DECLARE @barCodeNo nvarchar(32)
	DECLARE @EXPECTED_ARRIVAL_TIME DATETIME
	DECLARE @SUPPLIER_NAME nvarchar(100)
	DECLARE @PART_NICKNAME nvarchar(50)
	DECLARE @INNER_LOCATION nvarchar(50)--超市地址
	DECLARE @LINE_POSITION nvarchar(100)--线边地址

	SELECT
		@PLAN_RUNSHEET_NO = B.[PLAN_RUNSHEET_NO],
		@WM_NO = A.[WM_NO],
		@ZONE_NO = A.[ZONE_NO],
		@PLANT_ZONE = B.[PLANT_ZONE],
		@SUPPLIER_NUM = A.[SUPPLIER_NUM],
		@PART_NO = A.[PART_NO],
		@PART_CNAME = A.[PART_CNAME],
		@BOX_PARTS = A.[BOX_PARTS],
		@PICKUP_SEQ_NO = A.[PICKUP_SEQ_NO],
		@DLOC = A.[DLOC],
		@MEASURING_UNIT_NO = A.[MEASURING_UNIT_NO],
		@INHOUSE_PACKAGE_MODEL = A.[INHOUSE_PACKAGE_MODEL],
		@PLANT = A.[PLANT],
		@DOCK = A.[DOCK],
		@INHOUSE_PACKAGE = A.[INHOUSE_PACKAGE],
		@EXPECTED_ARRIVAL_TIME = B.[EXPECTED_ARRIVAL_TIME],
		@SUPPLIER_NAME = (SELECT TOP 1 [SUPPLIER_NAME] FROM [LES].[TM_BAS_SUPPLIER] C WITH (NOLOCK) WHERE A.[SUPPLIER_NUM] = C.[SUPPLIER_NUM]),
		@PART_NICKNAME = (SELECT TOP 1 [PART_NICKNAME] FROM [LES].[TM_BAS_MAINTAIN_PARTS] D WITH (NOLOCK) WHERE A.[PLANT] = D.[PLANT] AND A.[PART_NO] = D.[PART_NO]),
		@INNER_LOCATION = (SELECT TOP 1 [DLOC] FROM [LES].[TM_BAS_PARTS_STOCK] S WITH (NOLOCK) WHERE S.[PART_NO] = A.[PART_NO] AND S.[WM_NO] = A.[WM_NO] AND S.[ZONE_NO] = M.[REPACKAGE_ZONE] AND S.[PLANT] = A.[PLANT])--根据存储区查找对应翻包存储区的库位
	FROM [LES].[TT_SPM_DELIVERY_RUNSHEET_DETAIL] A WITH (NOLOCK)
	INNER JOIN [LES].[TT_SPM_DELIVERY_RUNSHEET] B WITH (NOLOCK) ON B.[PLAN_RUNSHEET_SN] = A.[PLAN_RUNSHEET_SN]
	LEFT JOIN [LES].[TM_WMM_ZONES] M WITH (NOLOCK) ON M.[ZONE_NO] = A.[ZONE_NO] AND M.[WM_NO] = A.[WM_NO] AND M.[PLANT] = A.[PLANT]
	WHERE A.[RUNSHEET_DETAIL_ID] = @RunsheetDetailId

	IF @PLANT_ZONE = '2016'
		BEGIN
			--获取库位地址
			SELECT TOP 1 @DLOC = [DLOC] FROM [LES].[TM_BAS_PARTS_STOCK] WITH (NOLOCK) WHERE  [PLANT] = @PLANT AND [ZONE_NO] = '2015' AND [PART_NO] = @PART_NO
			--获取超市地址
			SELECT TOP 1 @INNER_LOCATION = [DLOC] FROM [LES].[TM_BAS_PARTS_STOCK] WITH (NOLOCK) WHERE  [PLANT] = @PLANT AND [ZONE_NO] = '2016' AND [PART_NO] = @PART_NO
		END

	--根据目的存储区获取 生产线 然后再加上生产线 到拉动关系中获取 线边地址
	SELECT TOP 1
		@LINE_POSITION = L.[STORAGE_LOCATION] 
	FROM [LES].[TM_BAS_PARTS_STOCK] S WITH (NOLOCK)
	INNER JOIN [LES].[TM_BAS_MAINTAIN_INHOUSE_LOGISTIC_STANDARD] L WITH (NOLOCK) ON L.[PLANT] = S.[PLANT] AND L.[PART_NO] = S.[PART_NO] AND L.[ASSEMBLY_LINE] = S.[ASSEMBLY_LINE]
	WHERE S.[PLANT] = @PLANT AND S.[PART_NO] = @PART_NO AND S.[ZONE_NO] = ISNULL(@ZONE_NO, '')

	IF (SELECT COUNT(1) FROM 
		[LES].[TM_BAS_PARTS_STOCK] S WITH (NOLOCK)
		INNER JOIN [LES].[TM_BAS_MAINTAIN_INHOUSE_LOGISTIC_STANDARD] L WITH (NOLOCK)
		ON L.[PLANT]=S.[PLANT] AND L.[PART_NO] = S.[PART_NO] AND L.[ASSEMBLY_LINE] = S.[ASSEMBLY_LINE]
		WHERE S.[PLANT] = @PLANT AND S.[PART_NO] = @PART_NO AND S.[ZONE_NO] = ISNULL(@ZONE_NO, '')) > 1
		BEGIN
			SET @LINE_POSITION = @LINE_POSITION + '...'
		END

	WHILE (@PrintQty > 0)		--循环创建BARCODE
		BEGIN			
			EXEC [LES].[PROC_SPM_GET_NEXT_BARCODE_SEQUENCE] 'BARCODE', @barCodeNo OUTPUT

			INSERT INTO [LES].[TT_SPM_DELIVERY_RUNSHEET_BARCODE]
			(
				[PLAN_ASN_RUNSHEET_NO],
				[SUPPLIER_NUM],
				[PART_NO],
				[PART_CNAME],
				[BOX_PARTS],
				[PICKUP_SEQ_NO],
				[MEASURING_UNIT_NO],
				[INHOUSE_PACKAGE_MODEL],
				[REQUIRED_INBOUND_PACKAGE_QTY],
				[PLANT],
				[DOCK],
				[BARCODE_DATA],
				[REQUIRED_DATE],
				[SUPPLIER_NAME],
				[STORAGE_LOCATION],
				[PART_NICKNAME],
				[INNER_LOCATION],
				[LINE_POSITION],
				[PRINT_SUPPLEMENT]
			) 
			VALUES 
			(
				@PLAN_RUNSHEET_NO,
				@SUPPLIER_NUM,
				@PART_NO,
				@PART_CNAME,
				'DS',
				@PICKUP_SEQ_NO,
				@MEASURING_UNIT_NO,
				@INHOUSE_PACKAGE_MODEL,
				@INHOUSE_PACKAGE,
				@PLANT,
				@DOCK,
				@barCodeNo,
				@EXPECTED_ARRIVAL_TIME,
				@SUPPLIER_NAME,
				@DLOC,
				@PART_NICKNAME,
				@INNER_LOCATION,
				@LINE_POSITION,
				1
			)
		
			SET @PrintQty = @PrintQty - 1
		END
END