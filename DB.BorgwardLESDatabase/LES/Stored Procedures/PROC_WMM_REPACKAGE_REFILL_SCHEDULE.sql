


-- =============================================
-- Author:		houpeng
-- Create date: 2016-10-20
-- Description:	根据窗口时间，生成翻包计划
-- =============================================
CREATE PROCEDURE [LES].[PROC_WMM_REPACKAGE_REFILL_SCHEDULE]
	@PROCESS_DATETIME DATETIME = NULL
AS
BEGIN
	--DECLARE @PROCESS_DATETIME DATETIME = NULL
	--参数处理：默认当前时间
	IF(@PROCESS_DATETIME IS NULL)
	BEGIN
		SET @PROCESS_DATETIME = GETDATE()
	END
	DECLARE @PROCESS_DATE DATE = CONVERT(VARCHAR(10), @PROCESS_DATETIME, 111)
	DECLARE @PROCESS_TIME TIME = CONVERT(VARCHAR(8), @PROCESS_DATETIME, 8)

	--遍历可用的窗口时间
	DECLARE @SCHEDULE_IDENTITY INT
	DECLARE @R_PLANT NVARCHAR(5)
	DECLARE @R_ASSEMBLY_LINE NVARCHAR(10)
	DECLARE @R_XML XML
	DECLARE @LAST_RUN DATETIME

	DECLARE SCHEDULE_CURS1 CURSOR FOR
		SELECT   SCHEDULE_IDENTITY
				,PLANT
				,ASSEMBLY_LINE
				,CONVERT(XML,'<RS><R>'+REPLACE([ROUTES],',','</R><R>')+'</R></RS>') AS R_XML
				,ISNULL(LAST_RUN,'1900-1-1')
			FROM LES.TT_WMM_REPACKAGE_SCHEDULE
			WHERE IS_ACTIVE=1
			AND (
				(DATEDIFF(DAY,ISNULL(LAST_RUN,'1900-1-1'),@PROCESS_DATE)=1 AND DATEDIFF(MINUTE,WINDOWS_TIME,@PROCESS_TIME)>0)
				OR DATEDIFF(DAY,ISNULL(LAST_RUN,'1900-1-1'),@PROCESS_DATE)>1
			)
			
	OPEN SCHEDULE_CURS1
	FETCH NEXT FROM SCHEDULE_CURS1 INTO @SCHEDULE_IDENTITY,@R_PLANT,@R_ASSEMBLY_LINE,@R_XML,@LAST_RUN
	WHILE(@@FETCH_STATUS=0)
	BEGIN --窗口时间

		--遍历单个窗口时间下配置的翻包路径
		DECLARE @ROUTE NVARCHAR(30)
		DECLARE ROUTE_CURS1 CURSOR FOR
			SELECT DISTINCT R_NODE.R.value('.','NVARCHAR(30)') FROM @R_XML.nodes('/RS/R') AS R_NODE(R)
		OPEN ROUTE_CURS1
		FETCH NEXT FROM ROUTE_CURS1 INTO @ROUTE
		WHILE(@@FETCH_STATUS=0)
		BEGIN	--翻包路径

			IF(@ROUTE IS NULL OR @ROUTE='')
			BEGIN
				--若不存在此翻包路径，则跳过
				FETCH NEXT FROM ROUTE_CURS1 INTO @ROUTE
				CONTINUE
			END
			
			--翻包计划
			EXEC [LES].[PROC_WMM_REPACKAGE_PLAN_CREATE] @R_PLANT,@R_ASSEMBLY_LINE,@ROUTE,@PROCESS_DATETIME

			--补货计划
			EXEC [LES].[PROC_WMM_REFILL_PLAN_CREATE] @R_PLANT,@R_ASSEMBLY_LINE,@ROUTE,@PROCESS_DATETIME


			FETCH NEXT FROM ROUTE_CURS1 INTO @ROUTE
		END	--翻包路径
		CLOSE  ROUTE_CURS1
		DEALLOCATE ROUTE_CURS1

		--更新窗口时间
		UPDATE [LES].[TT_WMM_REPACKAGE_SCHEDULE] SET LAST_RUN=(
				CASE WHEN DATEDIFF(DAY,ISNULL([LAST_RUN],'1900-1-1'),@PROCESS_DATE)>1 THEN DATEADD(DAY,-1,@PROCESS_DATE)
					ELSE @PROCESS_DATE
				END
			)
			WHERE SCHEDULE_IDENTITY=@SCHEDULE_IDENTITY
	
		FETCH NEXT FROM SCHEDULE_CURS1 INTO @SCHEDULE_IDENTITY,@R_PLANT,@R_ASSEMBLY_LINE,@R_XML,@LAST_RUN
	END	--窗口时间
	CLOSE  SCHEDULE_CURS1
	DEALLOCATE SCHEDULE_CURS1

END